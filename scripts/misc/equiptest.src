use uo;
use os;

include ":tn:equipment";
include ":combat:armorZones";
include "include/say";
include ":timedScripts:timedScripts";

program EquipTest(mobile, item, start_up)
	var retval := 0;
	
	if ( start_up )
		// POL is starting up, yes the mobile can equip it
		retval := 1;
	elseif ( mobile.npctemplate )
		// NPCs can wear anything
		retval := 1;
	elseif ( mobile.cmdlevel )
		// CmdLevel > 0 can equip anything.
		retval := 1;
	elseif ( item.isa(POLCLASS_CONTAINER) )	
		// Makes backpacks always equippable.
		retval := 1;
	elseif ( (mobile.frozen) || (mobile.paralyzed) )
		// Frozen players cant equip stuff
		SendSysMessage(mobile, "Você esta incapaz de se movimentar para equipar isso.");
		return 0;
	else
		retval := 1;
	endif

	if ( item.graphic IN array{9920, 9930} )
		var mounted := GetEquipmentByLayer(mobile, 0x19);
		if ( !mounted )
			SendSysMessage(mobile, "Você não pode equipar essa arma desmontado.");
			return 0;
		endif
	endif

	if (GetObjProperty(item, "broken") == 1)
		SendSysMessageEx(mobile, "Você não pode equipar um item danificado.", SSM_FAIL);
		return 0;
	endif

	if (mobile.hidden)
		var zones := CS_GetArmorZone(item);
		if ("Body" IN zones || "Arms" IN zones || "Legs/feet" IN zones)
			SendSysMessage(mobile, "Você apareceu ao colocar "+item.desc+".");
			mobile.hidden := 0;
			PrintText(mobile, "*aparece*");
		endif
	endif
	
	if (TS_GetCooldown(mobile, "metamorfosedruida"))
		SendSysMessageEx(mobile, "Você não pode equipar itens enquanto estiver metamorfoseado.", SSM_FAIL);
		return 0;
	elseif (GetObjProperty(mobile, "desarmar") && item.isa(POLCLASS_WEAPON))
		SendSysMessageEx(mobile, "Você esta incapaz de armar-se.", SSM_FAIL);
		return 0;		
	endif

	if ( retval )
		SetObjProperty(item, "#EquippedOn", mobile.serial);
		//SetEquipmentMods(mobile, item);
		if (coverFace(item) && !mobile.npctemplate)
         SetObjproperty(mobile, "facecovered", 1);
			// if (!GetObjProperty(mobile, "realname"))
			// 	SetObjProperty(mobile, "realname", mobile.name);
			// endif
			// if (GetObjProperty(mobile, "description"))
			// 	SetObjProperty(mobile, "olddescription", GetObjProperty(mobile, "description"));
			// 	EraseObjProperty(mobile, "description");
			// endif
         SetName(mobile, mobile.name);
		endif
	else
		// Unable to equip but no reaso was given above. 
		SendSysMessage(mobile, "Unable to equip ->"+item.desc);
	endif
	
	return retval;
endprogram
