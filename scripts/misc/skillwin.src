/* $Id: skillwin.src 402 2005-10-04 09:33:15Z panosl $
 *
 * Purpose
 * This script is called when a player clicks the skill button in his paperdoll.
 *
 * Parameters
 * who:     Reference to the character who clicked the skill button
 *
 * Return value: Ignored
 *
 */
use uo;
use basic;

include ":charactercreation:characters";
include ":charactercreation:feats";
include ":tn:tngumps";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":gumps:yesno";
include "include/say";
include "include/client";
include ":gumps:requestgump";
include ":gumps:htmlgump";
//Standard text colors on TSSE
/////////////////////////////////////////////////////////////////
CONST GF_STDCOLOR		:= 1890;
CONST GF_HDRCOLOR		:= 1890;
CONST GF_SLCTDCOLOR		:= 1720;
var attr_list 	    := array{"Strength", "Dexterity", "Intelligence", "Wisdom", "Constitution"};

program core_skillWin(who)
	if (who.dead)
		SendSystemReport(who, "Estou morto e impossibilitado de realizar qualquer atividade.");
		return;
	elseif (GetObjProperty(who, "criandochar"))
		SendSystemReport(who, "Você deve terminar de criar seu personagem.");
		return;
	endif

	var delay := GetObjProperty(who, "#skillwindow");
	if(delay && delay > ReadGameClock())
      SendSysMessageEx(who, "Você ainda está fazendo outra coisa.");
      return;
	endif
	SetObjProperty(who, "#skillwindow", ReadGameClock() + 3);

	CloseGump(who, GUMPID_SKILLLIST);
	skillWindow(who);
   return 0;
endprogram


function skillWindow(who)
	var gump  := buildGump(who);
	var input := GFSendGump(who, gump);
	if (input[0] >= 1 && input[0] <= 50)
		Start_Script( AP_GetSkillScript( GetSkillNameByID( input[0] ) ) , who);
	elseif (input[0] > 100)
		var feat_list := GetFeatList(who);
		var feat := feat_list[input[0] - 100];
		feat := FeatInfo(feat);
      if (!feat.script)
         return SendSysMessageEx(who, "Este talento não é ativável.");
      endif
      print(feat.name);
      start_script(":charactercreation:commands/player/talento", array{who, feat.name});
	elseif (input[0] > 80)
      if (who.getPoints() < 0)
         return who.SetPoints(0);
      endif
		var attr := attr_list[input[0] - 80];
		AP_SetTrueStat(who, attr, AP_GetTrueStat(who, attr) + 1);
		who.SetPoints(-1);
	elseif(input[0] == 70)
		BuyTalents(who);
		//use feat
	endif
endfunction

function buildGump(who)
	var gump := GFCreateGump();
	GFDisposable(gump, 0);
	GFSetID(gump, GUMPID_SKILLLIST);
	GFResizePic(gump, 526, 42, 40000, 79, 48); //Options background
	GFResizePic(gump, 229, 13, 39925, 347, 465); //Default Background

	GFHTMLArea(gump, 287, 27, 60, 20, "<BASEFONT SIZE=5 COLOR=#ffffea>Pericias");

	var s_x := 240;
	var s_y := 52;
	foreach skill in (who.skillList())
		if (AP_GetSkillScript(skill) != error)
			var elem := AP_GetAttributeCfgElem(skill);
			var id := elem.SkillId;
			GFAddButton(gump, s_x, s_y, 2224, 2224, GF_CLOSE_BTN, id); //Use Skill
		endif
		GFGumpPic(gump, s_x+14, s_y-5, 40136, 0);
		var skill_name := skill;
		skill_name := StrReplace(skill_name, "_", " ");
      skill_name["Necromância"] := "Necrom.";
		GFTextLine(gump, s_x+18, s_y-3, 1152, skill_name);
		s_y += 30;
		sleepms(5);
	endforeach

	GFHTMLArea(gump, 442, 26, 60, 20, "<BASEFONT SIZE=5 COLOR=#ffffea>Talentos");
	var feat_list := GetFeatList(who);
	if (!feat_list[1])
		feat_list += struct{ "name" := "Nenhum"};
	endif

	var f_x := 395;
	var f_y := 53;
	foreach feat in feat_list
		var feat_info := feat;
		if (feat.name != "Nenhum")
			feat_info := FeatInfo(feat);
		endif
		
		if (feat_info.script)
			GFAddButton(gump, f_x, f_y, 2224, 2224, GF_CLOSE_BTN, 100+_feat_iter); //Usar Talento
		endif
		GFGumpPic(gump, f_x+14, f_y-5, 40135, 0);
		GFTextLine(gump, f_x+18, f_y-3, 1152, feat_info.name);
		if (feat.desc)
			GFTooltipText(gump, "{}".format(feat_info.desc));
		endif
      f_y += 30;
		// GFAddButton(gump, f_x+142, f_y-3, 22153, 22153, GF_CLOSE_BTN, 70); // Info
		sleepms(2);
	endforeach

	// GFGumpPic(gump, 581, 55, 4033, 1259);
	// GFGumpPic(gump, 577, 59, 4033, 1259);
	GFAddButton(gump, 579, 57, 4033, 4033, GF_CLOSE_BTN, 70); //Comprar Talento
	GFTooltipText(gump, "<BASEFONT color=#cece00 size=4>Lista de Talentos disponíveis");

	AttrGump(who, gump);

	return gump;
endfunction

function AttrGump(who, byref gump)
	// Attributes
	// GFHTMLArea(gump, 20, 11, 185, 20, "<BASEFONT SIZE=4 COLOR=#D3D3D3><center>Atributos");
	GFResizePic(gump, 6, 7, 5170, 219, 216);
	GFHTMLArea(gump, 52, 37, 131, 20, "<BASEFONT SIZE=5 COLOR=#4c4ca6>Status Lv.{}".format(who.getLevel()));
	GFGumpPic(gump, 153, 35, 5411, 0);

	var attr_points := who.getPoints();
	var attr_x := 52;
	var attr_y := 55;
	foreach attr in (attr_list)
		GFResizePic(gump, attr_x, attr_y, 3000, 120, 21);
		GFTextLine(gump, attr_x+4, attr_y, 2966, "{}:".format(translateAttribute(attr)));
		// GFResizePic(gump, attr_x+95, attr_y, 3000, 30, 21);
		GFTextLine(gump, attr_x+101, attr_y, 655, "{}".format(AP_GetTrueStat(who, attr)));

		if (attr_points)
			GFAddButton(gump, attr_x-21, attr_y-2, 55, 55, GF_CLOSE_BTN, 80+_attr_iter);
		endif

		attr_y += 24;
      sleepms(2);
	endforeach

	if (attr_points)
		GFHTMLArea(gump, 26, 177, 185, 20, "<BASEFONT SIZE=2 COLOR=#ffffd6><center>Você tem {} pontos para atribuir".format(attr_points));
	endif
endfunction

function BuyTalents(who)
	if (!who.GetTalentPoints())
		return SendSysMessageEx(who, "Sem pontos de talento disponiveis.", SSM_FAIL);
	endif

	var feat_list := GetNewFeatsList(who);
	var gump := GFCreateGump();
	
	GFPage(gump, 0);

	GFAddButton(gump, 388, 0, 2093, 248, GF_CLOSE_BTN, 0); //skill page
	GFResizePic(gump, 0, 0, 9380, 715, 508);
	GFGumpPicTiled(gump, 22, 36, 671, 11, 2091); //top decoration
	GFGumpPicTiled(gump, 23, 463, 671, 11, 2091); //bottom decoration
	
	GFPage(gump, 1);
   var x := 39;
   var y := 91;
	foreach feat in feat_list
		GFAddButton(gump, x, y, 2151, 2152, GF_CLOSE_BTN, _feat_iter+100);
      GFHTMLArea(gump, x+40, y+5, 150, 20, "<BASEFONT size=5>{}".format(feat.name));
		GFHTMLArea(gump, x+224, y-18, 413, 70, feat.desc, 1);

		if (_feat_iter % 5 != 0)
			GFGumpPic(gump, x-13, y+50, 57);
			GFGumpPicTiled(gump, x+11, y+50, 609, 11, 58); //divisoria
			GFGumpPic(gump, x+619, y+50, 59);

         y += 80;
		elseif (feat_list[_feat_iter+1])
			GFAddButton(gump, 651, 490, 2509, 2509, GF_PAGE_BTN, gump.cur_page+1); //next
			GFPage(gump, gump.cur_page+1);
			GFAddButton(gump, 45, 490, 2508, 2508, GF_PAGE_BTN, gump.cur_page-1); //previous
         y := 91;
		endif

		sleepms(2);
	endforeach

	GFHTMLArea(gump, 0, 485, 750, 20, "<BASEFONT size=0><center>Você tem {} pontos de talento.".format(who.GetTalentPoints()));

	var input := GFSendGump(who, gump);

	if (input[0] > 100)
		var feat_iter := input[0] - 100;
		var feat := feat_list[feat_iter];
		
		LearnFeat(who, feat.name);
	else
		skillWindow(who);
	endif
	// var feat_list := GetNewFeatsList(who);

	// GFPage(gump, 1);
	// foreach feat in feat_list

	// 	if (_feat_iter % 5 == 0 && )
	// 		//next
	// 		GFPage(gump, gump.cur_page+1);
	// 		//previous
	// 	else
	// 		//add separator
	// 	endif
	// 	sleepms(2);
	// endforeach
endfunction
