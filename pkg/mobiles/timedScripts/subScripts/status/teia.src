/* $Id: alcohol.src 87 2006-05-23 19:04:43Z sroyalty $
 *
 */

use uo;
use os;
use util;

include "include/client";
include ":timedscripts:timedScripts";
include "include/say";

function StartScript(mobile, attacker := 0, level := 0)
   var it := CreateItemAtLocation(mobile.x, mobile.y, mobile.z+1, 4317, 1);
   var it2 := CreateItemAtLocation(mobile.x, mobile.y, mobile.z+1, 4314, 1);

   var teias := GetObjProperty(mobile, "teias");
   if (!teias)
      teias := array{};
   endif

   teias := teias + array{it.serial, it2.serial};
   SetObjProperty(mobile, "teias", teias);

	PlaySoundEffect(mobile, 0x205);
	SendSystemReport(mobile, "Incapaz de se mover.");
	mobile.frozen := 1;
	
	return 1;
endfunction

function EndScript(mobile)
   var teias := GetObjProperty(mobile, "teias");
   foreach item in teias
      item := SystemFindObjectBySerial(item);
      Destroyitem(item);
      sleepms(2);
   endforeach
   EraseObjProperty(mobile, "teias");
	mobile.frozen := 0;
	return 1;
endfunction

program TimedScriptInit(params)
	var mobile := params[1];
	var type   := params[2];
	var level  := params[3];

   if (typeof(type) == "String")
      if (type == "End")
         return EndScript(mobile);
      elseif (type == "LogOff")
         return;
      endif
   endif

   return StartScript(mobile, type, level);
endprogram
