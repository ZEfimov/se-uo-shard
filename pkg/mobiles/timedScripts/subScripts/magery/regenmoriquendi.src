	/* $Id: alcohol.src 87 2006-05-23 19:04:43Z sroyalty $
 *
 */

use uo;
use os;
use util;

include "include/client";
include ":timedscripts:timedScripts";
include "include/say";
include ":attributes:attributes";

function StartScript(mobile, attacker := 0, level := 0)
   var healing_amount;
   while (mobile)
      if (!GetObjProperty(mobile, "noheal"))
         if (!healing_amount)
            healing_amount := cdbl(AP_GetVitalMaximumValue(mobile, HITS)) * 0.4;
         endif

         var last_autoheal := cint(GetObjProperty(mobile, "demon_heal"));
         if (last_autoheal <= ReadGameClock())
            var reduce_heal := 0;
            if (TS_GetCooldown(mobile, "divine_damage"))
               reduce_heal := cdbl(GetObjProperty(mobile, "#reduce_heal")); 
            endif
            SetObjProperty(mobile, "demon_heal", ReadGameClock() + 1);
            HealFLS(mobile, cint(healing_amount - (healing_amount * reduce_heal)));
         endif
      endif
      sleep(2);
   endwhile
   return 1;
endfunction

function EndScript(mobile)
   return 1;
endfunction

function LogOffScript(mobile)
   return 1;
endfunction

program TimedScriptInit(params)
	var mobile := params[1];
	var type   := params[2];
	var level  := params[3];

   if (typeof(type) == "String")
      if (type == "End")
         return EndScript(mobile);
      elseif (type == "LogOff")
         return LogOffScript(mobile);
      endif
   endif

   return StartScript(mobile, type, level);
endprogram
