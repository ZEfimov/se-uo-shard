use uo;
use os;

include "include/say";
include "include/utils";
include "include/sysEvent";
include ":attributes:attributes";

program Arrastar(who)
   _checkPid(who, "arrastando");
	var targ;

	if (TypeOf(who) == "Array")
		targ := who[2];
		who := who[1];
	endif

	if (!maolivre(who, 2))
		return 0;
	elseif (AP_GetVital(who, "Stamina") < 2)
		SendSysMessageEx(who, "Você esta cansado demais para arrastar algo...", SSM_FAIL);
		return;
	endif
	
	if (!targ)
		SendSysMessageEx( who, "Mover qual corpo?", SSM_REQUEST);
		targ := Target( who );
	endif

	var type;
	if (targ.isa(POLCLASS_CORPSE))
		type := "corpo"; // corpse
	elseif (GetObjProperty(targ, "corda") && !GetObjProperty(targ, "algemado"))
		type := "alvo"; // player
   elseif (GetObjProperty(targ, "deathdetails") || GetObjProperty(targ, "deathtime"))
		type := "corpo";
	else
		SendSysMessageEx(who, "Você não pode arrastar isto.", SSM_FAIL);
		return;
	endif
   
	if( CoordinateDistance(targ.x, targ.y, who.x, who.y) > 2 )
		return SendSysMessageEx( who, "O "+type+" esta muito longe.", SSM_FAIL );
	endif

   if (_getPid(targ, "arrastar", 1))
		return SendSysMessageEx( who, "Alguém já está arrastando este {}".format(type), SSM_FAIL );
   endif

   SendSysMessageEx(who, "Você está arrastando {}. Entre em modo Combate para parar de carregar.".format(targ.name), SSM_INFO);
   PrintText(who, "*arrastando {}*".format(type));
   while (!who.warmode && targ && !who.dead)
      if (Distance(who, targ) > 1)
         if (!(who.hidden || who.concealed) && RandomInt(10) <= 1)
            PrintText(who, "*arrastando {}*".format(type));
         endif

         if (!AP_ConsumeVital(who, "Stamina", 2))
            SendSysMessageEx(who, "Você esta cansado demais para arrastar algo...", SSM_FAIL);
            return;
         elseif( !MoveObjectToLocation(targ, who.x, who.y, who.z, who.realm, MOVEITEM_IGNOREMOVABLE));
            SendSysMessageEx( who, "Não eh possivel arrastar o "+type, SSM_FAIL); 
            return;
         endif
      endif
      sleepms(100);
   endwhile

   if (targ) 
      EraseObjProperty(targ, "#pid_arrastar");
   endif

   SendSysMessageEx(who, "Você parou de carregar o corpo.", SSM_INFO);
   EraseObjProperty(who, "#pid_arrastar");
endprogram
