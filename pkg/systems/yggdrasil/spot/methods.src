use os;
use uo;

include "include/arrays";
include ":yggdrasil:treasures";

program Install()
    return 1;
endprogram

exported function GetProps( spot )
    var data := struct{};
    data.+active       := GetObjProperty(spot, "active");
    data.+spawnpoints  := GetObjProperty(spot, "spawnpoints");
    data.+timeinterval := GetObjProperty(spot, "timeinterval");
    data.+evententry   := GetObjProperty(spot, "evententry");
    data.+treasures    := GetObjProperty(spot, "treasures");
    data.+spotgroup    := GetObjProperty(spot, "spotgroup");
    data.+activegroup  := GetObjProperty(spot, "activegroup");

    data.+yggdrasil := GetObjProperty(spot, "yggdrasil");

    return data;
endfunction

exported function SetProps( spot, data := struct{} )
    var old_data := GetProps(spot);
    if (data.name)
        spot.name := data.name;
    endif

    if (data.location)
        spot.location := data.location;
    endif

    if (data.active != error)
        old_data.active := data.active;
    endif

    if (data.spawnpoints)
        old_data.spawnpoints := data.spawnpoints;
    endif

    if (data.timeinterval)
        old_data.timeinterval := data.timeinterval;
    endif

    if (data.evententry)
        old_data.evententry := data.evententry;
    endif

    if (data.spotgroup)
        old_data.spotgroup := data.spotgroup;
    endif

    if (data.treasures)
        old_data.treasures := data.treasures;
    endif

    if (data.activegroup)
        old_data.activegroup := data.activegroup;
    endif
    old_data := ModelSpot(old_data);

    SetObjProperty(spot, "timeinterval", old_data.timeinterval);
    SetObjProperty(spot, "spawnpoints", old_data.spawnpoints);
    SetObjProperty(spot, "active", old_data.active);
    SetObjProperty(spot, "evententry", old_data.evententry);
    SetObjProperty(spot, "spotgroup", old_data.spotgroup);
    SetObjProperty(spot, "treasures", old_data.treasures);
    SetObjProperty(spot, "activegroup", old_data.activegroup);
endfunction

exported function yggdrasil ( spawnpoint, data := struct{})
     SetObjProperty(spawnpoint, "yggdrasil", data);
endfunction

function ModelSpot( data := struct{} )
    if (data.active != 1)
        data.active := 0;
    endif

    if (TypeOf(data.spawnpoints) != "Array")
        data.spawnpoints := array{};
    endif

    if (!data.timeinterval)
        data.timeinterval := 60;
    endif

    if (!data.evententry)
        data.evententry := "";
    endif 

    if (TypeOf(data.yggdrasil) != "Struct")
        data.yggdrasil := struct{};
    endif

    if (TypeOf(data.treasures) != "Array")
        data.treasures := array{};
    else
        foreach treasure in (data.treasures)
            treasure := ModelTreasure(treasure);
        endforeach
    endif

    if (TypeOf(data.activegroup) != "String")
        data.activegroup := "";
    endif

    return data;
endfunction

exported function AddSpawnPoint( spot, serial )
    var data := GetProps( spot );
    data.spawnpoints.append(serial);
    SetProps( spot, data );

    var spawnpoint := SystemFindObjectBySerial(serial);
    SetObjProperty(spawnpoint, "spotgroup", data.spotgroup);
    return 1;
endfunction

exported function RemoveSpawnPoint( spot, serial )
    var data  := GetProps( spot );
    var index := FindInArray(data.spawnpoints, serial);
    if (index)
        data.spawnpoints.Erase(index);
        SetProps( spot, data );
        var spawnpoint := SystemFindObjectBySerial(serial);
        if (spawnpoint)
            SetObjProperty(spawnpoint, "GM_FLAG", 1);
            DestroyItem(spawnpoint);
        endif
    endif
    return 1;
endfunction

exported function ForceSpawn( spot )
    var data  := GetProps( spot );
    foreach serial in (data.spawnpoints)
        var spawnpoint := SystemFindObjectBySerial(serial);
        spawnpoint.SpawnAll();
        sleepms(2);
    endforeach
endfunction

exported function ForceKill( spot )
    var data  := GetProps( spot );
    foreach serial in (data.spawnpoints)
        var spawnpoint := SystemFindObjectBySerial(serial);
        spawnpoint.KillAll();
        sleepms(2);
    endforeach
endfunction

exported function AddTreasure( spot, new_treasure )
    var data := GetProps( spot );

    var treasure := ModelTreasure(new_treasure);

    data.treasures.append(treasure);
    SetProps( spot, data );
    return 1;
endfunction

exported function UpdateTreasure( spot, serial, treasure_data )
    var data := GetProps( spot );
    var index := FindInArray(data.treasures, struct{"serial":=serial});
    if (!index)
        return 0;
    endif

    data.treasures[index] := treasure_data;

    SetProps( spot, data );
    return 1;
endfunction

exported function RemoveTreasure( spot, serial )
    var data  := GetProps( spot );
    var index := FindInArray(data.treasures, struct{"serial":=serial});
    if (index)
        data.treasures.Erase(index);
        SetProps( spot, data );
    endif
    return 1;
endfunction