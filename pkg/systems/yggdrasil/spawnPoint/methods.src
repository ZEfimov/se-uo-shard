/**
 * Included Functions
 * GetProps(spawnpoint)
 * SetProps(data[struct])
 * ModelSpawnPoint( data := struct{})
 *
 * Treasure
 * TreasureModel( data := struct{} )
 * AddTreasure(treasure)
 * RemoveTreasure(treasure)
 *
 * Spawns
 * FillQueue() - Fill the queue
 * DoSpawnQueue() - Spawn Next in queue
 * KillSpawn(index) - Kill monster by index
 * SpawnAll() - Spawn all monsters
 * KillAll() - Kill all monters
**/

use os;
use uo;

include "include/arrays";
include "include/dungeons";
include ":yggdrasil:spawnGroup";
include ":yggdrasil:spawnSpot";
include ":yggdrasil:boss";

program Install()
    return 1;
endprogram

exported function GetProps( spawnpoint )
   var data         := struct{};
   data.+spot_serial  := GetObjProperty(spawnpoint, "spot_serial");
   data.+level        := GetObjProperty(spawnpoint, "level");
   data.+type         := GetObjProperty(spawnpoint, "type");
   data.+range        := GetObjProperty(spawnpoint, "range");
   data.+mob_number   := GetObjProperty(spawnpoint, "mob_number");
   data.+spotgroup    := GetObjProperty(spawnpoint, "spotgroup");
   data.+monsters     := GetObjProperty(spawnpoint, "monsters");
   data.+spawngroup   := GetObjProperty(spawnpoint, "spawngroup");
   data.+spawnqueue   := GetObjProperty(spawnpoint, "spawnqueue");
   data.+groups       := GetObjProperty(spawnpoint, "groups");
   data.+diedmonsters := GetObjProperty(spawnpoint, "diedmonsters");

   if (!data.monsters)
      data.monsters := array{};
   endif
   if (!data.spawnqueue)
      data.spawnqueue := array{};
   endif

   if (!data.groups)
      data.groups := array{0};
   endif

  if (!data.diedmonsters)
      data.diedmonsters := array{};
   endif

   data.+yggdrasil := GetObjProperty(spawnpoint, "yggdrasil");

   return data;
endfunction

exported function SetProps( spawnpoint, data := struct{});
   var old_data := GetProps(spawnpoint);
   if (data.name)
      spawnpoint.name := data.name;
   endif

   if (data.location)
      spawnpoint.location := data.location;
   endif

   if (data.spot_serial)
      old_data.spot_serial := data.spot_serial;
   endif

   if (data.type)
      old_data.type := data.type;
   endif

   if (data.monsters)
      old_data.monsters := data.monsters;
   endif

   if (data.spawnqueue)
      old_data.spawnqueue := data.spawnqueue;
   endif

   if (data.range)
      old_data.range := cint(data.range);
   endif

   if ("level" in (data.Keys()))
      old_data.level := cint(data.level);
   endif

   if (data.groups)
      old_data.groups := data.groups;
   endif

   if (data.diedmonsters)
      old_data.diedmonsters := data.diedmonsters;
   endif

   if (data.mob_number)
      var group_data := GetSpotGroupData(old_data.spotgroup, 0);
      if (group_data)
        var group_mobs := cint(group_data.mob_number) - old_data.mob_number + cint(data.mob_number);
        SetSpotGroupData(old_data.spotgroup, struct{ "mob_number" := group_mobs});
      endif
      old_data.mob_number := cint(data.mob_number);
   endif

   if (data.evententry)
      old_data.evententry := data.evententry;
   endif

   if (data.spotgroup)
      old_data.spotgroup := data.spotgroup;
   endif

   old_data := ModelSpawnPoint(old_data);

   SetObjProperty(spawnpoint, "spot_serial", old_data.spot_serial);
   SetObjProperty(spawnpoint, "type", old_data.type);
   SetObjProperty(spawnpoint, "level", old_data.level);
   SetObjProperty(spawnpoint, "groups", old_data.groups);
   SetObjProperty(spawnpoint, "range", old_data.range);
   SetObjProperty(spawnpoint, "mob_number", old_data.mob_number);
   SetObjProperty(spawnpoint, "evententry", old_data.evententry);
   SetObjProperty(spawnpoint, "spawnqueue", old_data.spawnqueue);
   SetObjProperty(spawnpoint, "monsters", old_data.monsters);
   SetObjProperty(spawnpoint, "spotgroup", old_data.spotgroup);
   SetObjProperty(spawnpoint, "diedmonsters", old_data.diedmonsters);
endfunction

function ModelSpawnPoint( data := struct{})
   if (data.level != 1 && data.level != 2 && data.level != 3 && data.level != 4 && data.level != 5)
      data.level := 1;
   endif

   if (!data.spot_serial)
      data.spot_serial := 0;
   endif

   if (!data.range)
      data.range := 0;
   endif

   if (!data.evententry)
      data.evententry := "";
   endif 

   if (TypeOf(data.monsters) != "Array")
      data.monsters := array{};
   endif 

   if (TypeOf(data.spawnqueue) != "Array")
      data.spawnqueue := array{};
   endif 

   if (TypeOf(data.type) != "Array")
      data.type := array{"melee", "ranged", "spellcaster"};
   endif

   if (!data.mob_number)
      data.mob_number := 1;
   endif

   if (TypeOf(data.yggdrasil) != "Struct")
      data.yggdrasil := struct{};
   endif

   if (TypeOf(data.groups) != "Array")
      data.groups := array{0};
   else
      foreach g in (data.groups)
         data.groups[_g_iter] := cint(g);
         sleepms(2);
      endforeach
   endif 

   return data;
endfunction

// exported function RemoveTreasure(spawnpoint, treasure)
//    var treasures := GetProps(spawnpoint).treasures;
//    var index := FindInArray(treasures, treasure.ref_id);
//    if (index)
//       treasures.Erase(index);
//       SetProps(spawnpoint, struct{"treasures" := treasures});
//       return 1;
//    endif
//    return 0;
// endfunction

// exported function AddTreasure(spawnpoint, treasure)
//     var treasures := GetProps(spawnpoint).treasures;
//     treasures.append(treasure);
//     SetProps(spawnpoint, struct{"treasures" := treasures});
//     return 1;
// endfunction

exported function FillQueue(spawnpoint)
   var config      := GetProps(spawnpoint);
   var monsters    := config.monsters;

   // Remove monster from array
   // Set types and levels that is already spawned
   var types     := dictionary{};
   var levels    := dictionary{};
   var index_rem := array{};
   foreach monster_obj in monsters
      var monster := SystemFindObjectBySerial(monster_obj.serial);
      if (!monster || monster.master)
         index_rem += _monster_obj_iter;
      endif

      types[monster_obj.type]   := cint(types[monster_obj.type]) + 1;
      levels[monster_obj.level] := cint(levels[monster_obj.level]) + 1;
      sleepms(2);
   endforeach

   var spawn_queue := config.spawnqueue;
   foreach item in spawn_queue
      types[item.type]   := cint(types[item.type]) + 1;
      levels[item.level] := cint(levels[item.level]) + 1;
      sleepms(2);
   endforeach

   removeIndexes(monsters, index_rem);
   SetObjProperty(spawnpoint, "monsters", monsters);

   var spawn_size := monsters.size() + spawn_queue.size();

   // count recent died monsters
   var spot := SystemFindObjectBySerial(config.spot_serial);
   var spot_time := cint(GetObjProperty(spot, "timeinterval"));
   index_rem := array{};
   var diedmonsters := config.diedmonsters;
   foreach died in diedmonsters
      if (AddTimeTo(died.time, spot_time, "minutes") <= POLCore().systime)
         index_rem.append(_died_iter);
      else
         spawn_size += 1;
      endif
      sleepms(2);
   endforeach

   removeIndexes(diedmonsters, index_rem);
   SetObjProperty(spawnpoint, "diedmonsters", diedmonsters);

   // Spawn
   if (spawn_size >= config.mob_number)
      // print("Max Spawn size " + spawn_size);
      return; // Don't need to queue anything
   endif

   var spotgroup  := GetSpotGroupData(config.spotgroup, 0);
   if (!spotgroup.activegroup)
      SysLog(spawnpoint.serial+" no active spotgroup");
      return; //no active group
   endif
   var spawngroup := GetSpawnGroupData(spotgroup.activegroup, 0);
   if (!spawngroup)
      SysLog(spawnpoint.serial+" no active group");
      return; //no active group
   endif

   var daytime  := lower(GetGlobalProperty("dia"));
   var moonfase := lower(GetGlobalProperty("moonfase"));
   var n := 0;
   var spawn_number := cint(config.mob_number) - cint(spawn_size);
   for (n := 0; n < spawn_number; n := n + 1)
      var spawn_list := array{};
      spawn_queue := GetObjProperty(spawnpoint, "spawnqueue");

      foreach mob_config in (spawngroup.mob_list)
         if (mob_config.isBoss)
            mob_config.type := "boss";
         endif
         
         if ((!(mob_config.type in config.type)) || mob_config.power > config.level)
            continue;
         elseif (!(mob_config.daytime in array{ "always", "lua nova"}) && daytime != mob_config.daytime)
            continue;
         elseif (mob_config.daytime == "lua nova" && (moonfase != "nova" || daytime != "noite"))
            continue;
         elseif( !(cint(mob_config.mobgroup) in config.groups))
            continue;
         endif

         spawn_list.append(mob_config);
         sleepms(2);
      endforeach

      if (spawn_list.size() < 1)
         return; 
      endif
      
      if (config.type.size() > 1)
         if ("boss" in config.type && 1 < types["boss"])
            spawn_list := FindAllElemInArray(spawn_list, struct{ "type" := "boss" });
         else
            var preference := "melee";
            var z_diff := spot.z - spawnpoint.z;
            if (spot.z < 0 && z_diff < 0)
               z_diff := z_diff * -1;
            endif
            if (z_diff >= 10 && "ranged" in config.type)
               preference := "ranged";
            endif

            var calc_pref := Ceil(config.mob_number * 0.6);
            if (calc_pref < types[preference])
               spawn_list := FindAllElemInArray(spawn_list, struct{ "type" := preference });
            // elseif ("spellcaster" in config.type && types["spellcaster"] < Ceil(config.mob_number * 0.2))
            //    print("here2");
            //    spawn_list := FindAllElemInArray(spawn_list, struct{ "type" := "spellcaster" });
            endif
         endif
      endif

      var mob := spawn_list.RandomEntry();
      if (!mob) continue; endif;
      var to_spawn := struct{
         "template"   := mob.name,
         "type"       := mob.type,
         "level"      := mob.power,
         "daytime"    := mob.daytime,
         "bossParams" := mob.bossParams
      };

      if (mob.isBoss)
         to_spawn.+isBoss := mob.isBoss;
      endif
      
      spawn_queue += to_spawn;
      SetObjProperty(spawnpoint, "spawnqueue", spawn_queue);
      SetProps(spawnpoint, struct{ "spawnqueue" := spawn_queue });
      types[mob.type]   := cint(types[mob.type]) + 1;
      levels[mob.power] := cint(levels[mob.power]) + 1;
      sleepms(5);
   endfor
endfunction

exported function DoSpawnQueue(spawnpoint, pass := 0)
   var config  := GetProps(spawnpoint);
   var monster := config.spawnqueue[1];
   if (!monster)
      return 1;
   elseif (!pass && HasPlayerNear(spawnpoint.x, spawnpoint.y, spawnpoint.realm))
      return 1;
   endif

   var spot := SystemFindObjectBySerial(config.spot_serial);

   var i, x, y, z, spawned, success := 0;
   var loscheck := (config.loscheck == 1);
	while(success == 0)
		sleepms(8);
		if(config.range == 0 || i >= 5)
			x := spawnpoint.x;
			y := spawnpoint.y;
			z := spawnpoint.z;
		elseif(spawnpoint.z > spot.z)
			x := spawnpoint.x + (((RandomInt(3)-1)*-1) * RandomInt(1 + config.range));
			y := spawnpoint.y + (((RandomInt(3)-1)*-1) * RandomInt(1 + config.range));
			z := GetStandingHeight(x, y, spawnpoint.z-5, spawnpoint.realm);
		elseif(spawnpoint.z <= spot.z)
			x := spawnpoint.x + (((RandomInt(3)-1)*-1) * RandomInt(1 + config.range));
			y := spawnpoint.y + (((RandomInt(3)-1)*-1) * RandomInt(1 + config.range));
			z := spawnpoint.z+5;
		endif

		if(loscheck && !CheckLosAt(spawnpoint, x, y, z))
         i += 1;
         continue;
      endif

      spawned := CreateNpcFromTemplate(monster.template, x, y, z, 0, spawnpoint.realm);
      if (spawned)
         SetObjProperty(spawned, "spawnpoint", spawnpoint.serial);
         SetObjProperty(spawned, "spawnspot", config.spot_serial);
         SetObjProperty(spawned, "spawngroup", GetObjProperty(spot, "activegroup"));
         SetObjProperty(spawned, "range", config.range);
         
         if (monster.bossParams && monster.bossParams.size() >= 1)
            SetBoss(spawned, monster.bossParams);
         endif

         success := 1;
      endif
	endwhile

   if (spawned == error || !spawned.serial)
      return 0;
   endif

   monster.+serial := spawned.serial;
   config.spawnqueue.erase(1);
   config.monsters.append(monster);

   var update_conf := struct{
      "monsters" := config.monsters,
      "spawnqueue" := config.spawnqueue
   };

   SetProps(spawnpoint, update_conf);
   return 1;
endfunction

exported function KillSpawn(spawnpoint, index)
   var monsters := GetObjProperty(spawnpoint, "monsters");
   var monster_obj := monsters[index];
   if (!monster_obj)
      return 0;
   endif

   var monster := SystemFindObjectBySerial(monster_obj.serial);
   if (monster && !monster.master)
      SetObjProperty(monster, "summoned", 1);
      SetObjProperty(monster, "spawnkill", 1);
      for i := 1 to 2
         RevokePrivilege(monster, "invul" );
         ApplyRawDamage(monster, CInt(AP_GetVitalMaximumValue(monster, "Hits")));
         if (!monster || monster.dead) break; endif
         sleepms(10);
      endfor
   endif

   monsters.Erase(index);
   SetObjProperty(spawnpoint, "monsters", monsters);
endfunction

exported function SpawnAll(spawnpoint)
   var monsters := GetObjProperty(spawnpoint, "monsters");

   foreach m in monsters
      KillSpawn(spawnpoint, (monsters.size()+1) - _m_iter);
      sleepms(10);
   endforeach
   SetObjProperty(spawnpoint, "monsters", array{});
   SetObjProperty(spawnpoint, "diedmonsters", array{});

   FillQueue(spawnpoint);
   var queue := GetObjProperty(spawnpoint, "spawnqueue");
   foreach q in queue
      DoSpawnQueue(spawnpoint, 1);
      sleepms(10);
   endforeach
endfunction

exported function KillAll(spawnpoint)
   SetObjProperty(spawnpoint, "spawnqueue", array{});
   var monsters := GetObjProperty(spawnpoint, "monsters");
   
   foreach m in monsters
      KillSpawn(spawnpoint, (monsters.size()+1) - _m_iter);
      sleepms(10);
   endforeach

   SetObjProperty(spawnpoint, "monsters", array{});
endfunction

    // exported function SpawnTreasures(spawnpoint)
    //     var treasures := GetProps(spawnpoint).treasures;
    //     var treasures_spawned := GetObjProperty(spawnpoint, "spawned_treasures");

    // endfunction

    // exported function ResetTreasures(spawnpoint)

    // endfunction
