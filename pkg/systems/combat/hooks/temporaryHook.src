/////////////////////////////////////////////
//
// combathook.src
//
// Combat System Hooks
//
// Created: 2/26/2004 1:13AM
// Updated: 06/03/2007 1:13AM
//
// Responsavel por:
//
//
////////////////////////////////////////////

use uo;
use cfgfile;

include ":attributes:attributes";
include ":combat:armorZones";
include ":combat:combat";
include "include/packets";
include "include/facings";
include "include/say";
include "include/dungeons";
include "include/damage";
include "include/utils";
include ":timedscripts:timedScripts";
include ":combat:settings";
include ":mounts:mounts";
include ":fls_core:fls_characters";

var item_cff  := ReadConfigFile(":*:itemdesc");

//Global because they're important, and it keeps the function
//prototypes shorter and easier to read.
var wpn_a_elem;
var weapon;
var bonus_atk := 0;

program Attack(params)
   var attacker := params[1];
   var defender := params[2];
   var nofacecheck := cint(params[3]);
	weapon := attacker.weapon;

	if ( attacker == defender )
		ApplyRawDamage(attacker, 5000);
		return 1;
   elseif (attacker.my_template() == "orb")
      return 1;
   elseif (defender.my_template() == "orb" && defender.master != attacker)
      return 1;
	elseif ( !CheckLineOfSight(attacker, defender) )
		return 1;
	elseif ( GetObjProperty(attacker, "#Casting") || TS_GetCooldown(attacker, "noattack") )
		return 1;
   elseif ( TS_GetTimerInfo(attacker, "charmed") )
      if (GetObjProperty(attacker, "#charmed") == defender.serial)
         return 1;
      endif
   elseif ( GetEquipmentByLayer(attacker, LAYER_MOUNT) )
      if (AP_GetTrueSkill(attacker, MONTARIA) <= 4)
         return 1;
      endif
	endif

	if (attacker.acctname)
		if (!attacker.warmode)
			if(GetObjProperty(attacker, "#attackmsg") <= polcore().systime)
				SendSysMessageEx(attacker, "Você precisa estar em modo de combate para ameaçar alguém.", SSM_FAIL);
				SetObjProperty(attacker, "#attackmsg", polcore().systime+15);
			endif
			return 1;
		endif
	endif

	wpn_a_elem := GetItemCFGInfo(attacker, weapon);
	if ( attacker.frozen || attacker.paralyzed)
		SendSysMessageEx(attacker, "Você não consegue atacar.", SSM_FAIL);
		return 1;
	elseif ( (attacker.concealed) && (attacker.cmdlevel == 0) )
		attacker.concealed := 0;
	elseif ( !DistanceChecks(attacker, defender) )
		return 1;
	elseif ( !FacingChecks(attacker, defender) && !nofacecheck )
		return 1;
	elseif ( !AmmoCheck(attacker, defender) )
		return 1;
   elseif ( attacker.hidden)
      SendSystemReport(attacker, "*Golpe Furtivo*", SSM_INFO);
      bonus_atk += 1;
	endif

   if (weapon.isBow() && TS_GetTimerInfo(attacker, "hallucination"))
      SendSysMessageEx(attacker, "Você erra o alvo", SSM_FAIL);
      var mobiles := ListMobilesNearLocation(defender.x, defender.y, defender.z, 2);
      mobiles := RemoveFromArray(mobiles, attacker);
      defender := mobiles.randomentry();
   endif

   if ( CheckBlockDodge(defender, 0) )
		MissHit(attacker, defender);
      return;
   elseif (CheckBlockDodge(defender, 0, 1))
		PlayHitSounds(attacker, defender);
      return;
   elseif (GetObjProperty(defender, "#arrowrepulse") == 1 && weapon.isBow())
		MissHit(attacker, defender);
      return;
	endif

	var armor := CS_GetEquipmentInArmorZone(defender, CS_GetRandomArmorZone());
	armor     := CS_SelectArmored(armor, 1);

	PlayAttackAnimation(attacker, wpn_a_elem);

   if ( GetEquipmentByLayer(defender, LAYER_MOUNT) )
      MP_UnMount(defender);
   endif

   PlayHitSounds(attacker, defender);
   // ExpGain(attacker, 1);

   // if (!defender.npctemplate && defender.shield)
   //    if (wpn_a_elem.ProjectileAnim)
   //       defender.SetAchievement("ragendshield", 1);
   //    else
   // 	   defender.SetAchievement("shield", 1);
   //    endif
   // endif

   Run_Script_To_Completion(":combat:mainHitScript", array{attacker, defender, weapon, armor, wpn_a_elem });

	return 1;
endprogram

function MissHit(attacker, defender)
   if ( weapon.isBow() && !attacker.npctemplate )
      ArrowRecovery(attacker, defender);
   endif

   EraseObjProperty(attacker, "movement");

   PlayMissSound(attacker);
endfunction

function WearDownArmor(defender, armor)
	if ( armor )
		var armr_elem := CS_GetSettingsCfgElem("Armor");
		var wearchance := armr_elem.WearChance;
		// if ( HaveFeat(defender, "Conservar Equipamento"))
		// 	wearchance := wearchance/2;
		// 	if (wearchance < 1)
		// 		wearchance := 1;
		// 	endif
		// endif

		if ( RandomDiceRoll("1d100") <= wearchance )
			SendSysMessageEx(defender, "Seu equipamento foi danificado.");
			var armorhp := armor.hp;
			armorhp := armorhp - Cint(armr_elem.WearRate);
			if ( armorhp <= 0 )
				armor.hp := 0;
				SendSysMessageEx(defender, armor.desc+" foi destruida.");
				SetName(armor, armor.desc);
				MoveItemToContainer(armor, defender.backpack);
			elseif ( armor.hp > armor.maxhp )
				armor.hp := armor.maxhp;
			else
				armor.hp := armorhp;
            SetName(armor, armor.desc);
			endif
		endif
	endif
	
	return 1;
endfunction

function WearDownWeapon(attacker)
	if ( weapon.IsA(POLCLASS_WEAPON) && !attacker.npctemplate)
		if ( weapon.objtype == 0x1F020)
			return 1;
		endif
		var wpn_elem := CS_GetSettingsCfgElem("Weapons");
		var wearchance := CInt(wpn_elem.WearChance);
		var roll := RandomDiceRoll("1d100");
		if ( roll <= wearchance )
			var weaponhp := weapon.hp - 1;				
			if ( weaponhp <= 0 )
				weapon.hp := 0;
				SendSysMessageEx(attacker, weapon.desc+" quebrou.");
				MoveObjectToLocation(weapon, 1, 1, 1, attacker.realm, MOVEOBJECT_FORCELOCATION);
				SetName(weapon, weapon.desc);
				MoveItemToContainer(weapon, attacker.backpack);
			elseif ( weapon.hp > weapon.maxhp )
				weapon.hp := weapon.maxhp;
			else
				weapon.hp := weaponhp;
            SetName(weapon, weapon.desc);
			endif
		endif
		return 1;
	endif

	return 0;
endfunction

// Checa por munição ou faz efeito do npc arqueiro
function AmmoCheck(attacker, defender)
   var cfgfile := NPC_GetNPCConfig(attacker);
	if (weapon.isMagicWeapon())
		PlayMovingEffectEx(attacker, defender, cint(wpn_a_elem.ProjectileAnim), 5, 10, cint(wpn_a_elem.ProjectileColor));
		return 1;
	elseif ( !weapon.isBow() && !cfgfile.Ranged)
		return 1;
	endif

	if(attacker.npctemplate)
      if (wpn_a_elem.ProjectileAnim)
		   PlayMovingEffectEx(attacker, defender, cint(wpn_a_elem.ProjectileAnim), 10, 10, cint(wpn_a_elem.ProjectileColor));
      elseif (cfgfile.AttackTargetAnim)
         PlayObjectCenteredEffectEx( defender, cint(cfgfile.AttackTargetAnim), 10, 20, cint(cfgfile.ProjectileColor));
      else
		   PlayMovingEffectEx(attacker, defender, cint(cfgfile.ProjectileAnim), 10, 10, cint(cfgfile.ProjectileColor));
      endif

		return 1;
	endif

	var aljava := GetEquipmentByLayer(attacker, LAYER_CAPE);
	var ammo   := GetObjProperty(weapon, "ammo");
	if (!ammo)
		SendSysMessageEX(attacker, "Você não definiu o tipo de flecha que vai usar. [clique 2x no arco e na flecha]", SSM_FAIL);
		return 0;
	elseif ( !ConsumeSubstance( aljava, ammo, 1 ) )
		SendSysMessageEX(attacker, "Sua flecha acabou ou está fora da aljava.", SSM_FAIL);
		return 0;
	endif

   var cfg  := ReadConfigFile(":woodworking:itemdesc");
   var elem := FindConfigElem(cfg, ammo);
   var ammo_color := cint(elem.color);
   PlayMovingEffectEx(attacker, defender, cint(wpn_a_elem.ProjectileAnim), 10, 10, 0, ammo_color);
   return 1;
endfunction

// Cria as munição no chão
function ArrowRecovery(attacker, defender)
	var chance := 15;
   if (HaveFeat(attacker, "Flechas Intactas"))
      chance := 70;
   endif
   
	if ( RandomDiceRoll("1d100") <= chance )
		var ammo := GetObjProperty(weapon, "ammo");
		var point := RandomPlace(defender, 1);
		CreateItemAtLocation(point.x, point.y, defender.z, ammo, 1, defender.realm);
	endif
endfunction

// Checa direção q ta olhando
function FacingChecks(attacker, defender)
	var retval := 1;
	if ( !IsFacing(attacker, defender.x, defender.y) )
		if ( attacker.npctemplate )
			TurnObjectToward(attacker, defender.x, defender.y);
		else
			if ( IsBehind(attacker, defender.x, defender.y) )
				SendSysMessageEx(attacker, "Você está de costas para o inimigo!", SSM_FAIL);
				retval := 0;
			endif
		endif
	// elseif ( !IsFacing(defender, attacker.x, attacker.y) )
		// if ( defender.npctemplate )
		// 	TurnObjectToward(defender, attacker.x, attacker.y);
		// endif
	endif

	return retval;
endfunction