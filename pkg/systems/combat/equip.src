use uo;
use os;
use cfgfile;

include ":tn:equipment";
include ":tn:cooldown";
include "include/say";

program equipweapon(who, item)
	var descmod_item:= GetObjProperty(item,"descmod");

	if(descmod_item != error)
		var descmod := GetObjProperty(who,"descmod");
		if (!descmod)
			descmod := struct{};
		endif

		if (!(descmod_item in descmod))
			descmod.append(descmod_item);
			SetObjProperty(who,"descmod",descmod);
		endif

		var ori_name:= who.name;
		SetName(who, " ");
		SetName(who, ori_name);	
	endif
				
	if (who.npctemplate)
		return 1;
	endif

	set_critical(1);
	var hostiles := ListHostiles( who );
	if (hostiles.size() > 0 && !AP_ConsumeVital(who, STAMINA, 4))
		return 0;
	endif

	var ret := SetEquipmentMods(who, item);
	set_critical(0);

	return ret;
endprogram


function SetEquipmentMods(who, item)

	//SendSysMessage(who, "equip");

	var itemdesc := ReadConfigFile("::itemdesc");

	var restrict := Cint(itemdesc[item.objtype].GenderSpecific);
	if((restrict == 1) and (CheckGender(who) == 2))
		SendSysMessage(who, "Apenas mulheres podem vestir isto.");
		return 0;
	endif

	// if (item.isa(POLCLASS_ARMOR))
	// 	EraseObjProperty(who, "twohanded");
	// endif

	//sneak
	var penalty_list := GetConfigStringArray(itemdesc[item.objtype], "Penalty");
	foreach skill in penalty_list
		AP_ModifySkillMod(who, skill, -1);
	endforeach

	//verifica se eh magico
	var mods := GetObjProperty(item, "equip");
	if (!mods)
		mods := struct{};
	endif
//	SendSysMessage(who, " " + mods + " " + mods.keys().size());
	foreach mod in (mods.keys())
		if (GetObjProperty(item,"magic") == 1)
			continue;
		endif
		
		if (mod == "strmod")
			AP_ModifyStatMod(who, STRENGTH, Cint( mods["strmod"] ));
		elseif (mod == "dexmod")
			AP_ModifyStatMod(who, DEXTERITY, Cint( mods["dexmod"] ));
		elseif (mod == "intmod")
			AP_ModifyStatMod(who, INTELLIGENCE, Cint( mods["intmod"] ));
		elseif (mod == "conmod")
			AP_ModifyStatMod(who, CONSTITUTION, Cint( mods["conmod"] ) );
		elseif (mod == "wismod")
			AP_ModifyStatMod(who, WISDOM, Cint( mods["wismod"] ) );
		elseif (mod == "setprop")
			EraseObjProperty(who, "#" + GetObjProperty(item, "propname"));
		elseif (mod == "manamod")
			SetObjProperty(who, "#manamod", Cint(GetObjProperty(who, "#manamod")) + Cint( mods["manamod"] ));
			RecalcVitals(who);
		elseif (mod == "hitsmod")
			var prevhits := Cint(GetObjProperty(who, "#hitsmod"));
			SetObjProperty(who, "#hitsmod",  prevhits + Cint(mods["hitsmod"] ) );
			RecalcVitals(who);
		elseif (mod == "stammod")
			SetObjProperty(who, "#stammod", Cint(GetObjProperty(who, "#stammod")) + Cint( mods["stammod"] ) );
			RecalcVitals(who);
		else // se nao eh nada acima eh skillmod ou error :p
			AP_ModifySkillMod(who, mod, Cint(mods[mod]));
		endif
	endforeach

	return 1;
endfunction;