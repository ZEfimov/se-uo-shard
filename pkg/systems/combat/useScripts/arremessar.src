use uo;

include "include/say";
include ":attributes:attributes";
include ":charactercreation:feats";
include ":timedscripts:timedScripts";
include "include/facings";
include "include/damage";
include ":combat:combat";

program lancararma(who, weapon)
   if (weapon != who.weapon)
      return;
   elseif (AP_GetTrueSkill(who, LANCA_E_ALABARDA) < 3)
      return;
   endif
   
	SendSysMessageEX(who, "Selecione onde deve para lançar a arma. (esc para cancelar)", SSM_INFO);
	var targ := TargetCoordinates(who);

	if (!targ)
		SendSysMessageEX(who, "Cancelado.", SSM_FAIL);
		return;
   elseif (weapon != who.weapon)
      return;
	elseif (CoordinateDistance(who.x, who.y, targ.x, targ.y) > 10 || !CheckLosAt(who, targ.x, targ.y, targ.z))
		SendSysMessageEX(who, "O alvo esta muito distante ou você não pode vê-lo.", SSM_FAIL);
		return;
   elseif(!AP_ConsumeVital(who, STAMINA, 2))
      SendSysMessageEx(who, "Você está sem fôlego para arremessar a arma.", SSM_FAIL);
      return;
	endif
	
	
	PlayMovingEffectXYZ( who.x, who.y, who.z+3, targ.x, targ.y, targ.z, weapon.objtype, 10, 10, 0, who.realm);
	PlaySoundEffect(who, 0x5D3);
	PrintText(who, "*lança {}".format(weapon.desc));

	MoveObjectToLocation( weapon, targ.x, targ.y, targ.z+1 );

   var dmg := cint(RandomDiceRoll("2d3") + Ap_GetStat(who, STRENGTH, RETURN_BONUS));
	var altura := cint(who.z - targ.z);
	if (altura < 0)
		altura := 0;
	endif

   dmg += cint(altura/2);

   var mobiles := ListMobilesNearLocation(targ.x, targ.y, targ.z, 0);
	foreach mob in mobiles
      if (rollResistDice(mob, DEXTERITY) > 14)
         SendSystemReport(mob, "*esquiva*", SSM_FAIL, who);
         SendSystemReport(mob, "*esquiva*");
         continue;
      endif

      DamageFLS(mob, dmg, "Perfuração", who);
      sleepms(2);
   endforeach
endprogram 