use uo;
use os;

include "include/say";
include "include/arrays";
include ":faccao:npc";
include ":faccao:faccao";
include ":faccao:laws";
include ":gumps:gumps";
include ":tn:tngumps";
include ":gumps:yesno";
include ":unicos:item_template";

const BUTTON_BUY_UNIFORM := 0x2;
const BUTTON_ADD_PLAYER := 0x3;
const BUTTON_CHANGE_DOORS := 0x4;
const BUTTON_BAN_PLAYER := 0x6;
const BUTTON_CHANGE_PLAYER := 0x1222;

const BUTTON_ADD_NPC := 0x100;
const BUTTON_REM_NPC := 0x105;
const BUTTON_CONTRACT_NPC := 0x110;
const BUTTON_DISMISS_NPC := 0x115;

const BUTTON_ADD_LAW := 0x200;
const BUTTON_CHANGE_LAW := 0x225;

const BTN_FACCAO := 0x100;
const BTN_NEW_FACCAO := 0x4;

var guild;
var privilegies;
var is_gm_or_leader;
var priv_list := struct{
   "above_guards" := "Controle de Guardas;",
   "above_merchants" := "Controle de Mercadores;",
   "above_members" := "Controle de Membros;",
   "giveprivs" := "Controle de Permissões;",
   "contractnpc" := "Contratar de Agentes;",
   "fixitems" := "Controle de Objetos;",
   "unlockdoors" := "Controle de Portas;",
   "buyuniforms" := "Controle de Uniformes;"
};
var guild_obj;

program faccao(params)
   var who := params;
   var targ := params;
   var cmd;
   if (typeof(params) == "Array")
      who := params[1];
      targ := params[2];
      cmd := params[3];
   endif

   if (cmd == "guild")
      guild := FindGuild(targ);
   elseif (cmd)
      return Faccoes(who);
   else
      guild := targ.guild;
   endif

   if (!guild)
      return SendSysMessageEx(who, "Não pertence a uma facção", SSM_FAIL);
   endif

   CloseGump(who, 0x689, 0);

   var members := guild.members;
   guild_obj := GetFaccao(guild);
   var guild_leader;
   if (guild_obj.leader)
      guild_leader := SystemFindObjectBySerial(guild_obj.leader, SYSFIND_SEARCH_OFFLINE_MOBILES);
      if (!guild_leader) 
         guild.SetProp("leader", 0x0);
      endif
      members := RemoveFromArray(members, guild_leader);
   endif
   is_gm_or_leader := (who == guild_leader || who.cmdlevel);
   privilegies := guild.getProp(cstr(who.serial));
   var gump := GFCreateGump();
	
	GFPage(gump, 0);
   GFSetId(gump, 0x689);
	GFResizePic(gump, 26, 19, 39925, 573, 417); //background

	GFResizePic(gump, 228, 29, 9350, 171, 34);
	GFHtmlArea(gump, 232, 36, 165, 20, "<center><BASEFONT size=5>{}".format(guild_obj.name));
   
	GFResizePic(gump, 48, 67, 9350, 172, 326); //members background
	GFTextLine(gump, 88, 70, 0, "Membros");

   var leader_name := guild_leader.getName();
   if (!leader_name)
      leader_name := "-";
   elseif(who.cmdlevel)
      GFAddButton(gump, 49, 91, 5605, 5605, GF_CLOSE_BTN, BUTTON_CHANGE_PLAYER+guild_obj.leader);
   endif
   GFTextLine(gump, 67, 89, 49, "{}".format(leader_name));

   var y := 107;
   foreach mob in members
      // mob := SystemFindObjectBySerial(mob.serial, SYSFIND_SEARCH_OFFLINE_MOBILES);
      if (is_gm_or_leader || privilegies.above_members || privilegies.giveprivs)
         GFAddButton(gump, 49, y, 5605, 5605, GF_CLOSE_BTN, BUTTON_CHANGE_PLAYER+mob.serial);
      endif
      GFTextLine(gump, 67, y-2, 0, "{}".format(mob.getName()));
      var permissions := guild.GetProp(cstr(mob.serial));
      var permission_text := "";
      foreach priv in (permissions.keys())
         if (permissions[priv])
            permission_text += "{}<br>".format(priv_list[priv]);
         endif
         sleepms(2);
      endforeach

      if (!permission_text) 
         permission_text := "Nenhum privilégio";
      endif

      GFToolTipText(gump, permission_text);
      sleepms(2);
      y  += 18;
   endforeach
   y  += 18;

   if (is_gm_or_leader || privilegies.above_members)
      GFAddButton(gump, 67-10, y, 5605, 5605, GF_CLOSE_BTN, BUTTON_ADD_PLAYER);
      GFTextLine(gump, 67+10, y-2, 0, "Convidar");
   endif

   GFResizePic(gump, 228, 67, 9350, 172, 326); //leis background
   GFTextLine(gump, 268, 72, 0, "Leis em Vigor");
   y := 91;
   foreach law in (guild_obj.laws)
      if (is_gm_or_leader)
         GFAddButton(gump, 246-18, y, 5605, 5605, GF_CLOSE_BTN, BUTTON_CHANGE_LAW+_law_iter);
      endif
      var law_desc := "";
      if (law.item) law_desc := "Item: {}".format(law.item.desc); endif
      if (law.race) law_desc := "Raça: {}".format(law.race); endif
      if (law.guild) law_desc := "Facção: {}".format(FindGuild(law.guild).getProp("name")); endif
      GFTextLine(gump, 246, y-2, 0, "{}".format(law.name));
      GFToolTipText(gump, law_desc);
      sleepms(2);
      y  += 18;
   endforeach

   // if (is_gm_or_leader)
   //    GFAddButton(gump, 246-10, y, 5605, 5605, GF_CLOSE_BTN, BUTTON_ADD_LAW);
   //    GFTextLine(gump, 246+10, y-2, 0, "Adicionar");
   // endif
   
   var total_soldo := 0;
	GFResizePic(gump, 48+360, 67, 9350, 172, 326); //agents background
	GFTextLine(gump, 430, 69, 0, "Agentes Contratados");
   y := 91;
   foreach npc in (guild_obj.npcs_active)
      var mob := SystemFindObjectBySerial(npc.serial);
      GFAddButton(gump, 426-18, y, 5605, 5605, GF_CLOSE_BTN, BUTTON_DISMISS_NPC+_npc_iter);
      GFTextLine(gump, 426, y-2, 0, "{}".format(mob.name));
      var desc := "Soldo: {}<br>Tipo: {}".format(npc.price, npc.type);
      total_soldo += npc.price;
      GFToolTipText(gump, desc);
      sleepms(2);
      y  += 18;
   endforeach

   if (is_gm_or_leader || privilegies.contractnpc)
      // GFAddButton(gump, 426-10, y, 5605, 5605, GF_CLOSE_BTN, BUTTON_CONTRACT_NPC);
      // GFTextLine(gump, 426+10, y-2, 0, "Contratar");

      if (who.cmdlevel)
         GFAddButton(gump, 426-10, y+18, 5605, 5605, GF_CLOSE_BTN, BUTTON_ADD_NPC);
         GFTextLine(gump, 426+10, y-2+18, 0, "Adicionar NPC");
      endif
   endif
	
	// GFPage(gump, 2);
	// GFResizePic(gump, 227, 67, 9350, 346, 326);
	// GFAddButton(gump, 362, 357, 2071, 2073, GF_CLOSE_BTN, 0);


	GFResizePic(gump, 54, 398, 9350, 518, 29);
	GFGumpPic(gump, 491, 330, 40034, 0);
	GFGumpPic(gump, 59, 402, 40022, 0);
	GFTextLine(gump, 82, 401, 0, "{} moedas".format(cint(guild_obj.money)));
	GFTextLine(gump, 415, 401, 0, "Soldo Total: {} mo".format(total_soldo));

   // if (is_gm_or_leader || privilegies.above_members)
   //    GFAddButton(gump, 205-15, 401+3, 0x845, 0x846, GF_CLOSE_BTN, BUTTON_BAN_PLAYER);
   //    GFTextLine(gump, 205, 401, 0, "Banimentos");
   // endif

   if (is_gm_or_leader || privilegies.buyuniforms)
      GFAddButton(gump, 330-15, 401+3, 0x845, 0x846, GF_CLOSE_BTN, BUTTON_BUY_UNIFORM);
      GFTextLine(gump, 330, 401, 0, "Uniformes");
   endif

   if (is_gm_or_leader || privilegies.unlockdoors)
      GFAddButton(gump, 240-15, 401+3, 0x845, 0x846, GF_CLOSE_BTN, BUTTON_CHANGE_DOORS);
      GFTextLine(gump, 240, 401, 0, "Portas");
   endif

	var input := GFSendGump(who, gump);

   if (input[0] >= BUTTON_BUY_UNIFORM && input[0] <= BUTTON_BAN_PLAYER)
      guildHandler(who, input[0]);
      start_script(":faccao:faccao", array{who, guild.guildid, "guild"});
   elseif (input[0] >= BUTTON_CHANGE_PLAYER)
      playerHandler(who, input[0]);
      start_script(":faccao:faccao", array{who, guild.guildid, "guild"});
   elseif (input[0] >= BUTTON_ADD_NPC && input[0] <= BUTTON_ADD_NPC+0x100)
      npcHander(who, input[0]);
      start_script(":faccao:faccao", array{who, guild.guildid, "guild"});
   elseif (input[0] >= BUTTON_ADD_LAW && input[0] <= BUTTON_ADD_LAW+0x100)
      lawHandler(who, input[0]);
      start_script(":faccao:faccao", array{who, guild.guildid, "guild"});
   endif
endprogram

function Faccoes(who)
   var gump := GFCreateGump();
   GFResizePic(gump, 84, 90, 9260, 280, 260);

   GFGumpPicTiled(gump, 100,100,30,235,10460);
   GFGumpPicTiled(gump, 320,100,30,235,10460);
   
   var y := 120;

   var guilds := ListGuilds();
   foreach guild in guilds
      GFTextLine(gump, 170, y, 2100, "{}".format(guild.GetProp("name")));
      // GFTextLine(gump, 220, y, 2103, "{}".format(chardata.raca));
      GFAddButton(gump, 150, y+3, 0x845, 0x846, GF_CLOSE_BTN, BTN_FACCAO+_guild_iter);
      y+= 20;
      sleepms(2);
   endforeach
   y+=10;
   GFAddButton(gump, 172, y, 0x80E, 0x80E, GF_CLOSE_BTN, BTN_NEW_FACCAO);
   GFHTMLArea(gump,  170, y, 115, 25, "<basefont color=#0555D4><center>Nova Facção", 1);

   var input := GFSendGump(who, gump)[0];

   if (input >= BTN_FACCAO)
      var g := guilds[input - BTN_FACCAO];
      start_script(":faccao:faccao", array{who, g.guildid, "guild"});
   elseif (input == BTN_NEW_FACCAO)
      var questions := array{};
      questions.append(struct{ "title" := "Nome da facção?", "marked" := ""} );
      questions.append(struct{ "title" := "Area de poder? x1 y1 x2 y2", "marked" := ""} );
      questions.append(struct{ "title" := "Dinheiro inicial?", "marked" := "0"} );
      var output := QuestionsGump(who, questions);
      if (!output) return SendSysMessageEx(who, "Cancelado"); endif
      var area := SplitWords(output[2]);
      if (area.size() < 4) return SendSysMessageEx(who, "Area inválida"); endif

      CreateFaccao(output[1], area, cint(output[3]));
      Faccoes(who);
   endif
endfunction

function guildHandler(who, input)
   if (input == BUTTON_ADD_PLAYER)
      SendSysMessageEx(who, "Quem deseja convidar?", SSM_REQUEST);
      var targ := Target(who);
      if (!targ) 
         return SendSysMessageEx(who, "Cancelado"); 
      elseif (!targ.isA(POLCLASS_MOBILE) || !targ.connected)
         return SendSysMessageEx(who, "Alvo inválido ou não está online"); 
      elseif (targ.guild)
         return SendSysMessageEx(who, "Alvo já pertence a uma facção"); 
      endif

      SendSysMessageEx(who, "O alvo foi convidado para se juntar a facção", SSM_INFO); 

      var guild_name := guild.getProp("name");
      if (!YesNo(targ, "{} te convidou para se juntar a {}. Deseja aceitar?".format(who.name, guild_name)))
         return SendSysMessageEx(who, "{} recusou o convite.".format(targ.name), SSM_INFO); 
      endif

      AddMember(guild, targ);
      SendSysMessageEx(who, "{} se juntou a facção".format(targ.name), SSM_INFO); 
   elseif (input == BUTTON_CHANGE_DOORS)
      if (who.cmdlevel)
         SendSysMessageEx(who, "" +guild_obj.area);
      endif
      var area := guild_obj.area;
      SendSysMessageEx(who, "Qual porta deseja modificar?", SSM_REQUEST);
      var targ := Target(who);
      if (!targ)
         return SendSysMessageEx(who, "Cancelado"); 
      elseif (!targ.IsDoor())
         return SendSysMessageEx(who, "Isso não é uma porta"); 
      elseif (GetObjProperty(targ, "estalagem"))
         return SendSysMessageEx(who, "Você não pode mexer em portas de estalagem"); 
      elseif (!(targ.x >= area[1] && targ.x <= area[3] && targ.y >= area[2] && targ.y <= area[4]))
         return SendSysMessageEx(who, "Você está fora da sua área de poder."); 
      endif
      var options := array{};
      var ref_key := GetObjProperty(targ, "LockId");
      if (ref_key)
         options.append("Copiar chave");
         options.append("Remover Fechadura");
         options.append("Trocar Fechadura");
      else
         options.append("Adicionar Fechadura");
      endif
      options.append("Cancelar");
      var output := RadioGump(who, 300, 0, "Escolha uma opção", options, 0, 1);

      if (output["Copiar"])
         SendSysMessageEx(who, "Escolha a chave que vai receber a cópia", SSM_REQUEST);
         var thekey := Target(who);
         if (!thekey) return; endif

         if ( thekey.GetLockId() )
            SendSysMessageEx(who, "A chave destino não pode ser cópiada.", SSM_FAIL);
            return 0;
         endif
         thekey.SetLockId(ref_key);
         SendSysMessageEx(who, "Você copiou a chave com sucesso.", SSM_INFO);
      elseif (output["Remover"])
         targ.locked := 0;
         EraseObjProperty(targ, "level");
         EraseObjProperty(targ, "LockId");
      elseif (output["Adicionar"] || output["Trocar"]) 
         SendSysMessageEx(who, "Escolha a nova fechadura", SSM_REQUEST);
         var fechadura := Target(who);
         if (!fechadura) return; endif

         if (fechadura.objtype != 0xfa1)
            return SendSysMessageEx(who, "Isso não é uma fechadura."); 
         endif

         var lvl := Cint(GetObjProperty(fechadura, "level"));
         if (!lvl) lvl := 1; endif

         var lockid := CInt(GetObjProperty(fechadura, "LockId"));
         SetObjProperty(targ, "LockId", lockid);
         DestroyItem(fechadura);
         SendSysMessageEx(who, "Você instalou a fechadura com sucesso.", SSM_INFO);
      endif
   elseif (input == BUTTON_BUY_UNIFORM)
      var options := array{};
      if (who.cmdlevel)
         options.append("Novo Uniforme");
      endif
      
      foreach uniform in (guild_obj.uniforms)
         var desc := "{}".format(uniform.name);
         options.append(desc);
         sleepms(2);
      endforeach

      options.append("Cancelar");

      var output := RadioGump(who, 300, 0, "Escolha o uniforme que deseja criar a receita", options, 1, 1);

      if (output == 1 && who.cmdlevel)
         var pieces := array{};
         while (who.connected)
            SendSysMessageEx(who, "Escolha as peças do uniforme", SSM_REQUEST);
            var targ := Target(who);

            if (!targ) 
               break;
            endif

            var template := CreateItemTemplate(targ);
            pieces.append(template);
         endwhile

         if (pieces.size() < 1) return SendSysMessageEx(who, "Cancelado"); endif
         
         var questions := array{};
         questions.append(struct{ "title" := "Qual o nome do Uniforme?", "marked" := ""} );
         questions.append(struct{ "title" := "Qual craft pode fazer o uniforme?", "marked" := 1, "radio" := array{"Costura", "Ferraria", "Marcenaria"}} );
         questions.append(struct{ "title" := "Entre com o material: ID Quantidade", "marked" := "", "helper" := "metal, madeira, vidro, couro, pano, pontaflecha, demonbone"} );
         questions.append(struct{ "title" := "Entre com o material: ID Quantidade", "marked" := ""} );
         questions.append(struct{ "title" := "Entre com o material: ID Quantidade", "marked" := ""} );
         questions.append(struct{ "title" := "Entre com o material: ID Quantidade", "marked" := ""} );
         questions.append(struct{ "title" := "Entre com o material: ID Quantidade", "marked" := ""} );
         questions.append(struct{ "title" := "Entre com o material: ID Quantidade", "marked" := ""} );

         var form_output := QuestionsGump(who, questions);
         if (!form_output || !form_output[1]) return SendSysMessageEx(who, "Cancelado"); endif
         var materials := struct{};
         for i := 3 to 8
            if (!form_output[i]) break; endif
            var prop := SplitWords(form_output[i]);
            if (!prop || prop.size() < 2) 
               break; 
            elseif (!prop[1] || !prop[2])
               break;
            else
               materials[prop[1]] := prop[2];
            endif
            sleepms(2);
         endfor

         var new_uniform := struct{
            "name":= form_output[1],
            "materials":= materials,
            "pieces":= pieces,
            "craft_skill" := form_output[2]
         };
         var uniform_list := guild.GetProp("uniforms");
         uniform_list.append(new_uniform);
         guild.SetProp("uniforms", uniform_list);

         SendSysMessageEx(who, "Uniforme adicionado");
      elseif (output == options.size())
         return;
      else
         if (who.cmdlevel) 
            output -= 1;
            if (!YesNo(who, "O que deseja fazer?", "Criar Uniforme", "Remover Uniforme"))
               guild_obj.uniforms.Erase(output);
               guild.SetProp("uniforms", guild_obj.uniforms);
            endif
         endif

        if (!ConsumeSubstance(who.backpack, 0xE34, 1) && !who.cmdlevel)
            SendSysMessageEx(who, "Você precisa de um pergaminho em branco.", SSM_FAIL);
            return;
         endif

         var uniform := guild_obj.uniforms[output];
         var props := struct{};
         props.name := uniform.name;
         props.difficulty := 14;
         props.colored := 0;
         props.mark := 1;
         props.exceptional := 1;
         props.delay := 2;
         props.materials := uniform.materials;
         props.requires := array{};
         props.fastcraft := 0;
         props.craft_skill := uniform.craft_skill;
         props.pieces := uniform.pieces;

         var item := CreateItemInbackpack(who, 0x46AE);
         foreach prop in (props.keys())
            SetObjProperty(item, prop, props[prop]);
            sleepms(2);
         endforeach
      endif
   endif
endfunction

function playerHandler(who, input)
   input := input - BUTTON_CHANGE_PLAYER;
   var player := SystemFindObjectBySerial(input, SYSFIND_SEARCH_OFFLINE_MOBILES);
   var options := array{};
   if (is_gm_or_leader || privilegies.giveprivs)
      options.append("Alterar Privilégios");
   endif

   if (is_gm_or_leader)
      options.append("Tornar Líder");
   endif

   if (is_gm_or_leader || privilegies.above_members)
      options.append("Remover da Facção");
   endif

   options.append("Cancelar");

   if (options.size() < 1) return; endif

   var output := RadioGump(who, 300, 0, "Escolha uma opção", options, 0, 1);
   if (output["Alterar"])
      var privs := guild.GetProp(cstr(input));
      var checked := array{};
      var priv_opts := array{};
      foreach opt in (privs.keys())
         checked[_opt_iter] := privs[opt];
         priv_opts[_opt_iter] := priv_list[opt];
      endforeach

      var output := CheckBoxGump(who, 300, 0, "Escolha os privilégios que deseja alterar", priv_opts, checked);
      foreach opt in (privs.keys())
         var name := priv_opts[_opt_iter];
         privs[opt] := (name in output) >= 1;
      endforeach

      guild.SetProp(cstr(input), privs);
   elseif (output["Líder"])
      guild.SetProp("leader", input);
      SendSysMessageEx(who, "{} agora é o líder da facção".format(player.name), SSM_INFO); 
   elseif (output["Remover"])
      if (YesNo(who, "Deseja mesmo remover este personagem da facção?"))
         RemMember(guild, player);
         SendSysMessageEx(who, "{} removido da facção".format(player.name), SSM_INFO);
      endif
   endif

endfunction

function npcHander(who, input)

endfunction

function lawHandler(who, input)

endfunction