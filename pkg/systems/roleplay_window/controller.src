use uo;
use os;

include "include/sysEvent";
include "include/say";
include ":datafile:datafile";
include ":quest:quest";

program AC_start(who)
   detach();
   var oldpid := GetObjProperty(who, "roleplay_pid");
   if (oldpid)
      var process := GetProcess(oldpid);
      process.kill();
   endif

   SetObjProperty(who, "roleplay_pid", GetPid());
   RegisterForSpeechEvents( who, 3 );
   var last_see := array{};
   var already_saw := array{};
   SetObjProperty(who, "last_see", last_see);

   while (who.connected)	
      sleepms(15);
      var event := wait_for_event(5);
      var trig := 0;
      if (event.type == 0x456 && !(event.id in already_saw))
         if (event.text) last_see.Insert(1, event.text); endif
         already_saw.append(event.id);
         if (event.emote) PrintTextPrivate(who, "*{}*".format(event.emote), who); endif
         trig := 1;
         ProgressQuest(who, struct{ type := "Ir até Local", local := event.origin });
      elseif (event.type == 0x4785)
         ProgressQuest(who, event.search);
      endif

      var mobiles_around := ListMobilesInLineOfSight( who, 5 );
      foreach mob in mobiles_around
         if (!(mob.serial in already_saw))
            if( mob.acctname)
               // var cp_datafile := DFOpenDataFile(":charprofile:CPFile", DF_CREATE);
               // var cp_elem     := DFFindElement(cp_datafile, mob.serial, DF_CREATE);
               // var cp_profile  := DFGetProp(cp_elem, "Profile");
               // cp_profile := CChrz(cp_profile);
               var cp_profile;
               var chardata := GetObjProperty(mob, "chardata");
               if (chardata)
                  var altura := GetCharHeight(chardata.height);
                  var peso := GetCharWeight(chardata.weight);
                  if (mob.name["Desconhecido"])
                     cp_profile := "Altura {}, {}".format(altura, peso);
                  else
                     cp_profile := "{}, {}, {}".format(altura, peso, chardata.profile);
                  endif
               endif

               if (cp_profile)
                  var charname := SplitWords(mob.name)[1];
                  cp_profile := "<BASEFONT color=#AAAAAA>Você vê: <BASEFONT color=#FFA500>{} <BASEFONT color=#FAFAFA>{}".format(charname, cp_profile);
                  last_see.Insert(1, cp_profile);
                  already_saw.append(mob.serial);
                  SendSysMessage(who, "-> Personagem Avistado.");
                  trig := 1;
               endif
            elseif (GetObjProperty(mob, "roleplay_desc"))
               var description := GetObjProperty(mob, "roleplay_desc");
               description := "<BASEFONT color=#AAAAAA>Você vê: <BASEFONT color=#FFA500>{} <BASEFONT color=#FAFAFA>{}".format(mob.name, description);
               last_see.Insert(1, description);
               already_saw.append(mob.serial);
               SendSysMessage(who, "-> Criatura Avistada.");
               trig := 1;
            endif
         endif
         sleepms(10);
      endforeach
      
      if (trig)
         if (last_see.size() > 8)
            last_see.Erase(last_see.size());
         endif
         SetObjProperty(who, "last_see", last_see);
         CloseGump(who, 0x977, 1);
         sleepms(100);
      endif
   endwhile
		
	return 1;
endprogram

function GetCharHeight(h)
   case (h)
      1: return "Alto";
      2: return "Mediano";
      3: return "Baixo";
   endcase
endfunction


function GetCharWeight(w)
   case (w)
      1: return "Gordo";
      2: return "Esbelto";
      3: return "Magro";
   endcase
endfunction