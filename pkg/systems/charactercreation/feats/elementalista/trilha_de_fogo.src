use uo;
use os;

include ":magery:tnmagery";
include "include/damage";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedscripts:timedScripts";
include ":magery:basicMagery";
include ":combat:combat";

var stalagmites := array{0x08E0, 0x08E1, 0x08E4, 0x08E5, 0x08E0};

program SpellScript(params)
	var who       := params[1];
   var spellinfo := params[2];
	var targ      := params[3];
	params := 0; // No longer needed

   var direction := GetFacing(who.x, who.y, targ.x, targ.y);
   var face := cint(direction / 2);
   if (face > 4)
      face /= 2;
   endif

   var flame;
   var dmg_type := DMG_FIRE;
   if (spellinfo.Name["Fogo"])
      flame := 0x3996;
      if (face % 2)
         flame := 0x398C;
      endif
   elseif (spellinfo.Name["Veneno"])
      flame := 0x3915;
      if (face % 2)
         flame := 0x3920;
      endif
      dmg_type := DMG_POISON;
   elseif (spellinfo.Name["Eletrecidade"])
      flame := 0x3967;
      if (face % 2)
       flame := 0x3979;
      endif
      dmg_type := DMG_ENERGY;
   endif

   var pos := GetCoordsInLine(who.x, who.y, targ.x, targ.y);
   pos.Erase(1);
   var z := who.z;

   var duration := 15;
   if (spellinfo.duration) duration := spellinfo.duration; endif

   foreach p in pos
      if (targ.z > z)
         z += 1;
      elseif (targ.z < z)
         z -= 1;
      endif
      var item := CreateItemAtLocation(p.x, p.y, z, cint(flame));
      item.SetDuration(duration);
      item.SetOwner(who);
      item.SetDamage(spellinfo.PowerMod[2]);
      foreach mobile in ListMobilesNearLocationEx( item.x, item.y, item.z, 0, LISTEX_FLAG_NORMAL|LISTEX_FLAG_HIDDEN)
			if (!CheckIfisAlly(who, mobile));
            if (CheckBlockDodge(mobile, 0, 1))
               continue;
            endif
            DamageFLS(mobile, spellinfo.PowerMod[1], dmg_type, who);
         endif
      endforeach
      sleepms(100);
   endforeach


	return 1;
endprogram 