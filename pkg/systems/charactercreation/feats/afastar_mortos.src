use uo;
use os;

include ":magery:tnmagery";
include ":attributes:attributes";
include ":ghaia:ghaiaUtil";
include "include/client";
include "include/sounds";
include "include/say";
include ":tn:npcutil";
include ":yggdrasil:boss";
include "include/utils";

program SpellScript(params)
	var who := params[1];
	params := 0; // No longer needed
   
   var diceroll := rollAttrDice(who, WISDOM) + GetSkillBonus(who, MILAGRE_DE_NECROMANCIA);
	PlaySoundEffect(who, SFX_SPELL_STRENGTH);
	var newmobiles := ListMobilesNearLocation( who.x, who.y, who.z, 5);
	foreach mob in newmobiles
      sleepms(2);
      if (!mob.npctemplate || getnpccategory(mob.my_template()) != "morto-vivo" || isBoss(mob)) 
         continue; 
      endif
      if (rollResistDice(mob, WISDOM) <= diceroll)
         printtext(mob, "*!*");
         ghaiaFlee(who, mob);
      else
         SendSystemReport(mob, "*resistido*", SSM_FAIL, who);
      endif
   endforeach
	
	return 1;
endprogram