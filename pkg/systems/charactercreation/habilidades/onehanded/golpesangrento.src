
use uo;

include ":fls_core:packets";
include ":combat:combat";
include ":blood:blood";
include "include/damage";
include ":timedScripts:timedScripts";
include ":tn:cooldown";

program doubleattack(params)
	var attacker	:= params[1];
	var defender	:= params[2];
	var weapon	:= params[3];
	var armor	:= params[4];
	var basedamage	:= params[5];
	var rawdamage	:= params[6];
	params := 0; // Not needed anymore.
	
	if (lower(attacker.weapon.attribute) != "meleecombat1h")
		return;
	endif
	
	PlayAttackAnimation(attacker);
	PlayHitSounds(attacker, defender);
	
	PrintTextAbove(attacker, "*acerta uma artéria*");
	SendSysMessageEX(defender, "Voce nao sera capaz de se curar pelos proximos segundos", SSM_FAIL);

	var time := cint(Ap_GetSkill(attacker, TACTICS) / 5);
	var bonus := cdbl(Ap_GetSkill(attacker, TACTICS) / 100);
	
	TS_StartTimer(defender, "nocure", time);
	var dmgList := dictionary;
	dmgList := getNewDamageList(attacker);
	var total := cint(CalculateTotalDamage(defender, basedamage, attacker, dmgList));
	DamageFLS(defender, cint(total+(total*bonus)), DMG_FORCED, attacker);
endprogram  
