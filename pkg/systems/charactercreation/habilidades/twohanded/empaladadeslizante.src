
use uo;

include "include/say";
include ":attributes:attributes";
include ":timedScripts:timedScripts";
include "include/facings";
include "include/damage";
include ":tn:combat";
include ":combat:combat";

program empaladadeslizante(who)
	SendSysMessageEX(who, "Selecione o alvo para lancar a arma.", SSM_INFO);
	var targ := TargetCoordinates(who);
	while (1)
		if (CoordinateDistance(who.x, who.y, targ.x, targ.y) > 5 || !CheckLosAt(who, targ.x, targ.y, targ.z) || who.z+5 < targ.z)
			SendSysMessageEX(who, "O alvo esta muito distante. Tente um mais proximo", SSM_FAIL);
		else
			break;
		endif
		targ := TargetCoordinates(who);
	endwhile
	
	who.setwarmode(1);
	who.setfacing(GetFacing(who.x, who.y, targ.x, targ.y));
	PlayAttackAnimation(who);
	
	if(lower(who.weapon.attribute) != "meleecombat2h")
		SendSysMessageEX(who, "Cancelado.", SSM_FAIL);
		return;
	endif
	
	var distancia := GetCoordsInLine(who.x, who.y, targ.x, targ.y);
	var alreadydamaged := array{};
	var dmgList := dictionary;
	dmgList := getNewDamageList(who);
	var basedamage := CalculateRawDamage(who, who.weapon);
	
	var bonus := cdbl(0.2+(Ap_GetSkill(who, TACTICS) / 80));
	
	foreach pontos in distancia
		var mobiles := ListMobilesNearLocation(pontos.x, pontos.y, who.z, 1, who.realm);
		mobiles.append(ListMobilesNearLocationEx( pontos.x, pontos.y, who.z, 1, LISTEX_FLAG_HIDDEN, who.realm ));
		foreach mobile in mobiles
			if (!(mobile.serial in alreadydamaged) && (mobile != who) && !(mobile in who.party.members) && !(mobile.invul))
				PrintText(mobile, "*empalado*");
				alreadydamaged.Append(mobile.serial);
				var total := cint(CalculateTotalDamage(mobile, basedamage, who, dmgList));
				DamageFLS(mobile, cint(total+(total*bonus)), DMG_FORCED, who);
			endif
			MoveObjectToLocation( who, pontos.x, pontos.y, who.z);
			sleepms(70);
		endforeach
		sleepms(20);
	endforeach

	

endprogram 
