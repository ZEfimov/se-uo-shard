use uo;
use os;
use math;
use vitals;

include "include/say";
include ":mounts:mounts";

program cabelo(who)

	if (who.hidden)
		SendSysMessageEx(who, "Você não pode fazer isso escondido.", SSM_FAIL);
		return 0;
	elseif (GetEquipmentByLayer(who, 0x19))
		SendSysMessageEx(who, "Você não consegue fazer isso montado.", SSM_FAIL);
		return 0;
	endif

	SendSysMessageEx(who, "Selecione a cerca que deseja pular.", SSM_REQUEST);
	var targ := TargetCoordinates(who);
	if(!targ)
		SendSysMessage(who, "Cancelado.");
		return 0;
	endif

	if (CoordinateDistance(who.x, who.y, targ.x, targ.y) > 1 || abs(who.z-targ.z) > 10)
		SendSysMessageEx(who, "Você esta muito longe disto.", SSM_FAIL);
		return 0;
	endif

	if (!targ.objtype)
		SendSysMessageEx(who, "Alvo invalido.", SSM_FAIL);
		return 0;
	endif

	var grafico := targ.graphic;
	if (!grafico)
		grafico := targ.objtype;
	endif

	if (grafico IN {947, 948, 949, 2101, 2102, 2103, 2140, 2141, 2142, 2143, 2144, 2145, 2146, 2147, 2148, 2149, 2167, 2168, 2186, 2187, 8657, 8658, 8659, 8660})
		if (GetVital(who, "Stamina") < GetVitalMaximumValue(who, "Stamina"))
			SendSysMessageEx(who, "Você precisa estar bastante disposto para fazer isto.", SSM_FAIL);
			return 0;
		endif
		SetVital(who, "Stamina", 0);
		who.hidden := 0;
		if (GetEquipmentByLayer(who, 0x19))
			MP_Unmount(who);
		endif
      var last_loc := struct{ "x" := who.x, "z" := who.z, "y" := who.y };
		MoveObjectToLocation(who, targ.x, targ.y, targ.z, who.realm, MOVEOBJECT_FORCELOCATION);
		PrintText(who, "*pulou a cerca*");
      foreach monster in ListMobilesNearLocationEx(targ.x, targ.y, targ.z, 12, LISTEX_FLAG_NORMAL|LISTEX_FLAG_HIDDEN|LISTEX_FLAG_NPC_ONLY)
         if (GetObjProperty(monster, "guild"))
            SendEvent(monster, struct{ "type" := 0x78456, source := who, "last_loc" := last_loc});
         endif
         sleepms(2);
      endforeach
	else
		SendSysMessageEx(who, "Você não pode pular isto.", SSM_FAIL);
		return 0;
	endif

endprogram
