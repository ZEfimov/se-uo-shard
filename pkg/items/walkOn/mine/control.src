/*
 * $Id: control.src 935 2005-11-07 00:34:53Z austin $
 *
 */

use uo;
use os;
use util;

include "include/sounds";
include "include/damage";
include ":timedscripts:timedScripts";

program controlScript(field)
	SetObjProperty(field, "#PID", GetPid());
	var creator := field.GetOwner();
	if ( creator )
		SetScriptController(creator);
	endif

	var dmg_dice := GetObjProperty(field, "DamageDice");
	if ( !dmg_dice )
		dmg_dice := "1d5";
	endif

	while ( field )
      var nearby := ListMobilesNearLocationEX(field.x, field.y, field.z, 0, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN, field.realm);
      if (nearby[1])
		   MineTrigger(field, nearby[1], dmg_dice);
      endif

		// if ( ReadGameClock() % 3 == 0 )
		// 	// Play once every 3 seconds.
		// 	PlaySoundEffect(field, 0x1DE);
		// endif

		var event := Wait_For_Event(1);
		if ( event )
			//Someone just walked onto the tile.
			MineTrigger(field, event, dmg_dice);
		endif
		
		if ( field.IsExpired() )
			DestroyItem(field);
			return 1;
		endif
	endwhile
endprogram

function MineTrigger(field, mobile, dmg_dice)
   var owner := field.GetOwner();
   if (owner == mobile);
      return;
   elseif (CheckIfIsAlly(owner, mobile))
      return;
   endif

   var diff := cint(field.GetDifficulty());
   var resist_dice := cint(rollResistDice(mobile, CONSTITUTION));
   if (field.GetEffect() == DMG_FIRE)
      var dmg := RandomDiceRoll(dmg_dice)+8; // - resistance ?
      DamageFLS(mobile, dmg, DMG_FIRE, owner);
      var explosion := SelectRandomExplosion();
      PlaySoundEffect(mobile, SFX_EXPLOSION_11);
      PlayObjectCenteredEffect(mobile, explosion, 0, 5);
      if (resist_dice <= diff)
         TS_StartTimer(mobile, "tombar", 1);
      else
         SendSystemReport(mobile, "*resistiu*", SSM_FAIL, owner);
         SendSystemReport(mobile, "*resistiu*", SSM_INFO);
      endif
   else
      var dmg := RandomDiceRoll(dmg_dice)+5; // - resistance ?
      DamageFLS(mobile, dmg, DMG_COLD, owner);
      if (resist_dice <= diff)
         TS_StartTimer(mobile, "frozen", 4);
      else
         SendSystemReport(mobile, "*resistiu*", SSM_FAIL, owner);
         SendSystemReport(mobile, "*resistiu*", SSM_INFO);
      endif
   endif

   var links := field.GetAllLinks();
   foreach item in links
      DestroyItem(item);
   endforeach
endfunction
