/*
 * $Id: control.src 935 2005-11-07 00:34:53Z austin $
 *
 */

use uo;
use os;
use util;

include "include/damage";
include ":tn:cooldown";

program controlScript(field)
	SetObjProperty(field, "#PID", GetPid());

	var creator := field.GetOwner();
	if ( creator )
		SetScriptController(creator);
	endif

	while ( field )
		BurnNearbyMobiles(field);

		if ( ReadGameClock() % 3 == 0 )
			// Play once every 3 seconds.
			PlaySoundEffect(field, 0x1DE);
		endif

		var event := Wait_For_Event(1);
		if ( event )
			//Someone just walked onto the tile.
			BurnMobile(event, field.GetDamage());
		endif
		
		if ( field.IsExpired() )
			DestroyItem(field);
			return 1;
		endif
	endwhile
endprogram

function BurnNearbyMobiles(field)
   var creator := field.GetOwner();
	var nearby := ListMobilesNearLocationEX(field.x, field.y, field.z, 0, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN, field.realm);
	foreach mobile in ( nearby )
      if (creator != mobile && !(mobile.npctemplate && creator.npctemplate))
		   BurnMobile(mobile, field.GetDamage(), creator);
      endif
		sleepms(2);
	endforeach

	return (nearby.size());
endfunction

function BurnMobile(mobile, dmg_dice, creator := 0)
   if (!GetCooldown(mobile, "walkDamage"))
      SetCooldown(mobile, "walkDamage", 2);
	   DamageFLS(mobile, dmg_dice, DMG_FIRE, creator);
   endif
	return 1;
endfunction
