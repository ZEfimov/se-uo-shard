use uo;
use util;

include ":attributes:attributes";
include "include/say";


program soltar(me)
	if (!GetObjProperty(me, "corda"))
		SendSysMessage(me, "Esse comando não ira lhe ajudar em nada.");
		return;
	elseif (me.dead)
		SendSysMessage(me, "Você esta morto...");
		return;
	elseif (AP_GetVital(me, "Stamina") < 10)
		SendSysMessage(me, "Você esta cansado demais para tentar algo...");
		return;
	endif

	var diff := cint(GetObjProperty(me, "corda"));
	var dice_roll;
	if (AP_GetStat(me, DEXTERITY) > AP_GetStat(me, STRENGTH))
		dice_roll := rollAttrDice(me, DEXTERITY, 0, struct{ "get_critical" := 1, "failure" := 2});
	else
		dice_roll := rollAttrDice(me, STRENGTH, 0, struct{ "get_critical" := 1, "failure" := 2});
	endif

	var check := dice_roll.dice + GetSkillBonus(me, IMOBILIZACAO);

	if (check >= diff)
		if (dice_roll.critical == 1)
			SendSysMessage(me, "Você conseguiu se soltar discretamente...");
			var corda := CreateItemInBackpack (me, "rope");
			SetObjProperty(corda, "diff", GetObjProperty(me, "corda"));
		else
			PrintText(me, "*consegue se soltar*");
		endif
		EraseObjProperty (me, "corda");
		me.frozen := 0;
	elseif (dice_roll.critical == -1)
		PrintText(me, "*tentando se soltar*");
	else
		SendSysMessageEx(me, "Você falhou...", SSM_FAIL);
	endif

	ConsumeVital(me, "Stamina", 2000);

endprogram
