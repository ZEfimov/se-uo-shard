use uo;
use os;

include "include/client";
include "include/say";
include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":timedscripts:poisonUtil";
include ":charactercreation:habilidades";

program UseLeech(who, leech)
	if (!TemHabilidade(who, "Sangria"))
		SendSysMessageEX(who, "Voce precisa da habilidade Sangria para usar isso.", SSM_FAIL);
		return;
	endif

	SendSysMessage(who, "Em quem voce deseja usar isto?");
	var targ := target(who);
	
	if ( !targ.acctname )
		SendSysMessage(who, "Cancelado");
		return;
	elseif ( !targ.poisoned )
		SendSysMessage(who, targ.name+" nao esta envenenado.");
		return;
	elseif ( Distance(who, targ) > 2 )
		SendSysMessage(who, targ.name+" esta muito distante.");
		return;
	endif
	
	if ( SkillCheck(who, "Medicine", -1) > 0 )
		SendSysMessage(who, "Voce aplicou a sanguessuga em "+targ.name);
		var healing := AP_GetSkill(who, MEDICINE);
		TS_CurePoison(targ, CInt(healing*2), "P", CInt(healing/15));
	else
		SendSysMessage(who, "Voce nao foi capaz de aplicar a sanguessuga corretamente em "+targ.name);
	endif
	
	SubtractAmount(leech, 1);
endprogram