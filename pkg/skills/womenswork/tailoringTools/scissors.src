use uo;
use cfgfile;

include "include/objtype";
include ":attributes:attributes";
include "include/client";
include ":itemutils:canAccess";
include ":itemutils:toolWear";

var scissors_cfg := ReadConfigFile("scissors");
var cfg2 := ReadConfigFile("tailoring");
var cfg3 := ReadConfigFile("leather");

program use_scissors( who, scissors )
	EraseObjProperty(who, "IsMeditating");
	if(!Accessible(who, scissors))
		return;
	endif
	SendSysMessageEx(who, "Escolha um item para cortar.");
	var cloth := Target(who);

	if(!cloth)
		SendSysMessageEx(who, "Cancelado.");
		return;
	elseif(!Accessible(who, cloth))
		SendSysMessageEx(who, "Você não pode usar isto.");
		return;
	elseif(cloth.movable == 0)
		SendSysMessageEx(who, "Você não pode usar isto.");
		return;
	elseif(cloth in ListEquippedItems(who))
		SendSysMessageEx(who, "Você não pode cortar algo que esta vestindo!");
		return;
	endif

	var material := Cint(scissors_cfg[cloth.objtype].material);
	var clr := cloth.color;
	var cont := cloth.container;
	var newitem := Cint(scissors_cfg[cloth.objtype].newitem);
	var amt, newcloth;

	if(material)
		amt := (material * cloth.amount);
		if(SubtractAmount(cloth, amt))
			newcloth := CreateItemAtLocation(who.x, who.y, who.z, newitem, amt, who.realm);
			newcloth.color := clr;
			if (cont)
				MoveItemToContainer(newcloth, cont);
			else
				MoveItemToContainer(newcloth, who.backpack);
			endif
			CheckToolWear(who, scissors, COSTURA);
			PlaySoundEffect(who, 0x0249);
			SendSysMessageEx(who, "Você usou a tesoura para cortar o material.");
			return;
		else
			SendSysMessageEx(who, "Você não pode cortar isto.");
			return;
		endif
	else
		material := Cint(cfg2[cloth.objtype].material);
		var leftover := 0x1766;
		if(!material)
			if(cloth.objtype == UOBJ_BLANK_SCROLL)
				makecartoes(who, cloth);
				return;
			endif
			material := Cint(cfg3[cloth.objtype].material);
			leftover := 0x1067;
			
			if(!material)
				SendSysMessageEx (who, "Você não pode usar as tesouras nisso.");
				return;
			endif
		endif
		PlaySoundEffect(who, 0x0249);
		amt := CInt((material * AP_GetSkill(who,COSTURA) * 75) / 10000);
		if(amt < 1)
			amt := 1;
		endif
		if(DestroyItem(cloth))
			newcloth := CreateItemInContainer(who.backpack, leftover, amt);
			CheckToolWear(who, scissors, COSTURA);
			if(newcloth)
				newcloth.color := clr;
			endif
		else
			SendSysMessageEx(who, "You can't use scissors on that.");
			return;
		endif
	endif

endprogram


function MakeCartoes (who, papel)
	if (!ReserveItem (papel))
		SendSysMessageEx (who, "Você não pode usar este item!");
		return;
	endif

	var sx := who.x;
	var sy := who.y;
	repeat
		for i := 1 to 3
			PlaySoundEffect (who, 0x0249);
			sleep (1);
		endfor

		if (!SubtractAmount (papel, 1))
			PrintTextAbovePrivate (who, "*Voce não pode usar estes pergaminhos!*", who);
			return;
		endif


			if (!CreateItemInContainer (who.backpack, 0xEE23, 1))
				PrintTextAbovePrivate (who, "*Sua mochila esta cheia!*", who);
				return;
			endif

			SendSysMessageEx (who, "Você fez os cartoes.");

		if (!papel)
			SendSysMessageEx (who, "Você não tem mais pergaminhos");
			return;
		endif
	until (who.x != sx or who.y != sy);

	SendSysMessageEx (who, "Você parou de fazer.");
endfunction

