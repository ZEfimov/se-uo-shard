

use cfgfile;
use uo;
use os;
use util;

include "include/client";
include "include/say";
include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":blood:blood";
include ":survival:skinning";
include ":ghaia:ghaiaUtil";
include ":gumps:yesno";
include ":fls_core:fls_characters";
include ":loot:include/lootParser";
include ":unicos:item_template";

program skinning(parms)
	var who     := parms[1];
	var cadaver := parms[2];
	var blade   := parms[3];
	
	if (who.weapon != blade)
		var cfg := ReadConfigFile(":combat:itemdesc");
		var equippedright := GetEquipmentByLayer(who, 0x01);
		var equippedleft := GetEquipmentByLayer(who, 0x02);
		var twohanded := Cint(cfg[equippedleft.objtype].TwoHanded);
		if (equippedright || equippedleft || twohanded)
			SendSysMessageEx(who, "Você precisa das duas mãos livres!", SSM_FAIL);
			return 0;
		endif
	endif

	if ( GetEquipmentByLayer(who, LAYER_MOUNT) )
		SendSysMessageEx(who, "Você não pode fazer cortar o corpo montado.");
		return 0;
	elseif((!Accessible(who, cadaver)) || (!CheckLineOfSight(who, cadaver)))
		SendSysMessageEx(who, "Você não pode usar isto.");
		return;
	elseif(Distance(who, cadaver) > 2)
		SendSysMessageEx(who, "Você não esta perto o bastante do alvo.");
		return;
	endif

	detach();
	if (GetObjProperty(cadaver, "cut") == "1")
		SendSysMessageEx( who, "Você não pode pegar mais coisas desse corpo." );
		foreach thing in EnumerateItemsInContainer(cadaver)
			MoveObjectToLocation(thing, cadaver.x, cadaver.y, cadaver.z, cadaver.realm, MOVEOBJECT_FORCELOCATION);
         sleepms(2);
		endforeach
		//DestroyItem(cadaver);
		return;
	elseif((GetObjProperty(cadaver,"npctemplate")== error) && (GetObjProperty(cadaver, "morto")!=1))
      if (YesNo(who, "Você deseja mesmo matar o alvo?", "Sim: o alvo poderá negar o pedido e você não pode sair de perto do corpo", "Nao: nenhuma consequencia.")) //quer matar mesmo
         start_script(":death:permadeath/permanentdeath", array{who, cadaver, blade});
         SendSysMessageEx(who, "Aguarde até que o alvo responda. Afaste-se do corpo para cancelar a ação.", SSM_FAIL);
      endif
      return;
   endif
   PrintText(who, "*escalpelando "+cadaver.desc+"*");
   SetObjProperty(cadaver, "morto",1);
   if(cadaver.corpsetype == 223)
      CreateItemInContainer(cadaver, 0x9f1, 2);
      SendSysMessageEx(who, "Você colocou os itens no corpo.", SSM_INFO);
      MakeBleed(cadaver);
      return;
   endif

   var conf    := ReadConfigFile(":ghaia:npcdesc");
   var tmplate := GetObjProperty(cadaver, "npctemplate");
   //var tmplate := cadaver.NPCTemplate();

   if(!tmplate)
      SendSysMessageEx(who, "Escalpelou o corpo, mas nada foi aproveitado.", SSM_FAIL);
      MakeBleed(cadaver);
      return;
   endif
   SetObjProperty(cadaver, "cut","1");

   var elem :=  NPC_GetNPCConfig(tmplate);
   var corpseitm := elem.corpseitm;

   if(!corpseitm)
      MakeBleed(cadaver);
      SendSysMessageEx(who, "Parece que nada pode ser aproveitado aqui", SSM_FAIL);
      return;
   endif

   corpseitm := SplitWords(corpseitm);
   var corpseamt := SplitWords(elem.corpseamt);

   var minamt;
   var bonusdif := 0;

   if (blade.objtype == 0x0ec4)//se for skinning knife da bonus
      bonusdif := -2;
   endif

   var found := 0;
   foreach thing in corpseitm
      var dif := bonusdif + CINT(GetSkinningDifficulty(thing) );
      if (dif < 5) dif := 9; endif

      minamt := GetMinSkinningAmt(thing);
      var dice_roll := RandomInt(100);
      if(dice_roll <= dif)
         if (minamt > corpseamt[_thing_iter])
            minamt := corpseamt[_thing_iter];
         endif
         var item := CreateItemInContainer(cadaver, cstr(thing), cint(minamt) );			
         found := 1;
      endif
      sleepms(2);
   endforeach

   var skinning_info := GetObjProperty(cadaver, "skinninginfo");
   if (skinning_info)
      foreach loot in skinning_info
         var dice_roll := RandomInt(100);
         var dif := loot.chance;
         if (dice_roll <= dif)
            if (loot.by_group)
               Loot_Generate(cadaver.backpack, loot.by_group, loot);
            else
               var item := CreateItemAtLocationFromTemplate(cadaver.x, cadaver.y, cadaver.z, _DEFAULT_REALM, loot.item );
               MoveItemToContainer(item, cadaver.backpack);
               item.movable := 1;
            endif
         endif
         sleepms(2);
      endforeach
   endif

   if (found)
      // ExpGain(who, "minor");
      who.SetAchievement("skinning", 1);
      SendSysMessageEx(who, "Escalpelou o corpo.", SSM_INFO);
   else
      SendSysMessageEx(who, "Nada foi aproveitado.", SSM_FAIL);
   endif

   MakeBleed(cadaver);
endprogram

