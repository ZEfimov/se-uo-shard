/* $Id: bedroll.src 905 2005-11-05 08:20:50Z muaddiblsd $
 *
 */

use uo;
use os;

include ":itemutils:canAccess";
include "include/say";

CONST LOGOUT_WAIT 	:= 30;
CONST UOBJECT_CAMPFIRE  := 0xde3;

program use_Bedroll( who, bedroll )

	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");
	if(!can_access(who, bedroll))
		return 0;
	endif
	if(bedroll.movable == 0)
		SendSysMessage(who, "Você não pode usar isto.");
		return 0;
	endif
	var items := ListItemsNearLocationOfType( who.x,who.y,who.z, 2, UOBJECT_CAMPFIRE, who.realm );
	if( len(items) == 0 )
		SendSysMessageEx( who, "Você precisa de um acampamento proximo para usar isto.", SSM_FAIL );
		return 0;
	else
		SendSysMessageEx( who, "Você deve permanecer aqui para preparar o acampamento.", SSM_FAIL );
	endif

	var x := who.x;
	var y := who.y;
	var z := who.z;

	var ttl := LOGOUT_WAIT;

	while( ttl > 0 )
		if( (who.x != x) || (who.y != y) || (who.z != z) )
			SendSysMessageEx( who, "Você abandonou suas preparacoes para o acampamento.", SSM_FAIL);
			return 0;
		endif
		SendSysMessageEx( who, "Você deve esperar " + ttl + " segundos para deslogar.", SSM_FAIL );
		ttl := ttl - 5;
		sleep(5);
	endwhile

	SetObjProperty(who,"#camped",1);
	DisconnectClient( who );

endprogram
