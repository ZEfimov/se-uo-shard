
include ":attributes:attributeConstants";
include ":attributes:attributes";
include "include/objtype";
include ":itemutils:canAccess";
include ":timeUtils:time";
include ":death:death";
include "include/say";
include "include/damage";

//const UOBJ_LIGHTSOURCE := 0x1647;
const UOBJ_TORCH := 0x0a12;
const enchanted := 0;

program UseWood( who, wood )
	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");
	if( !can_access(who, wood) )
		return 0;
	elseif( wood.movable == 0 )
		SendSysMessageEx(who, "Você não pode acender isso.", SSM_FAIL);
		return 0;
	elseif( !Accessible(who, wood) )
		SendSysMessageEx( who, "Você não alcanca isto!", SSM_FAIL );
		return 0;
	elseif( wood.container )
		SendSysMessageEx( who, "Coloque no chão primeiro!", SSM_FAIL );
		return 0;
	endif
	// var camptimer := 0;//GetObjProperty(who, "CampTimer");
	// if( camptimer )
	// 	if( camptimer > ReadGameClock() )
	// 		SendSysMessageEX(who, "Você deve esperar alguns segundos para fazer isto novamente.", SSM_FAIL);
	// 		return 0;
	// 	endif
	// endif
	// SetObjProperty(who, "CampTimer", ReadGameClock() + 3);
	PrintText(who, "*tenta acender uma fogueira*");
	var difficulty := 10;
	if (GetEquipmentByLayer(who, 0x02).objtype == 2578)
		difficulty := 0;
	endif

	var dice_roll := randomint(100);
	if( dice_roll > difficulty )
		var x := wood.x;
		var y := wood.y;
		var z := wood.z;
  
		Detach();
		PrintText( who, "*acendendo uma fogueira*");
		sleep (5);
		var campfire := CreateItemAtLocation( x, y, z, UOBJ_CAMPFIRE, 1 );
      SetObjProperty(campfire, "camping", wood.serial);
		var torch := CreateItemAtLocation( x, y, z-4, UOBJ_TORCH, 1 );
		SetName(torch, "fogueira");
		PrintText( campfire, "Um acampamento foi preparado.");
		
		campfire.movable := 0;
		var time := (AP_GetStat(who, WISDOM, RETURN_BONUS) + 10) * 2;
		time := cint(time * wood.amount);
		DestroyItem(wood);
		var tempocura := 0;
		var continuehealing := 1;
		var healpoints := 2;

		while( time > 0 )
         var itemnearby := ListItemsNearLocationOfType( x, y, z, 12, 0xDE3 );
         if (itemnearby.size() > 1)
            SendSysMessageEx(who, "Já existe outro acampamento pronto por perto.", SSM_FAIL );
            break;
         endif

			if (GetObjProperty(campfire, "encantado") == 1 && !enchanted)
				healpoints := healpoints+2;
				PrintText(campfire, "*brilha mais forte*");
            enchanted := 1;
            EraseObjProperty(campfire, "encantado");
			endif

			time -= 1;
			tempocura += 1;
			if (tempocura == 5)
				tempocura := 0;
				var nearbypeople := ListMobilesNearLocation( x ,y, z, 6);
				foreach char in (nearbypeople)
					if (char.warmode)
						continuehealing := 0;
                  break;
					endif
					sleepms(2);
				endforeach

				if (continuehealing == 1)
					foreach char in (nearbypeople)
                  var hungerlvl := AP_GetVital(who, "Hunger") / 100;

						healFLS(char, cint(cdbl(AP_GetVitalMaximumValue(char, HITS)) * hungerlvl), 0);
						healMana(char, cint(cdbl(AP_GetVitalMaximumValue(char, MANA)) * hungerlvl));
						healStamina(char, cint(cdbl(AP_GetVitalMaximumValue(char, STAMINA)) * hungerlvl));
                  sleepms(2);
					endforeach
            else
					PrintText(campfire, "*algo atrapalha o acampamento*");
				endif
			endif
			sleep(3);
			continuehealing := 1;
		endwhile
		DestroyItem(campfire);
		DestroyItem(torch);

		campfire := CreateItemAtLocation(x,y,z,UOBJ_ASHES,1, who.realm);
		x := campfire.x;
		y := campfire.y;
		sleep(60);
		if( (campfire.x == x) && (campfire.y == y) )
			DestroyItem(campfire);
		endif
	else
		PrintText( who, "*acendendo uma fogueira*");
		sleep(3);
		SendSysMessageEx( who, "Você não conseguiu acender o fogo", SSM_FAIL );
	endif

endprogram
