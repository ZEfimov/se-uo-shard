
use uo;
use os;
use cfgfile;

include "include/say";
include ":attributes:attributes";

program change(who, item )
  var cfg  := ReadConfigFile(":*:itemdesc");
  var elem := FindConfigElem(cfg, item.objtype);
  if(item.graphic != item.objtype)
      if(elem.originsnd)
      PlaySoundEffect(who, elem.originsnd );
    endif
    item.graphic := item.objtype;
    return;
  endif

  if(elem.changesnd)
    PlaySoundEffect(who, elem.changesnd);
  endif

  item.graphic := elem.change;
  if (item.graphic != 6222)
    return;
  endif

  foreach balao in ListItemsNearLocation(item.x, item.y, item.z, 1)
    if ( !(balao.objtype in array{6194, 6195, 6196}) )
      continue;
    endif
    
    foreach beker in ListItemsNearLocation(item.x, item.y, item.z, 1)
      if ( beker.objtype in array{6192, 6193} )
          Destilar(balao, beker, who, item);
      endif
    endforeach
    break;
  endforeach

  item.graphic := item.objtype;
endprogram

function Destilar(balao, beker, who, item)
  var extrato := CINT(GetObjProperty(balao, "extrato"));
  if (!extrato)
    SendSysmessageEx(who, "Nenhum extrato no balao.", SSM_FAIL);
    return;
  endif
  ReleaseItem(item);
   item.movable := 0;
   balao.movable := 0;
   beker.movable := 0;

   sleep(5);
   PrintText(balao, "O extrato comeca a ferver.");
   sleep(10);
   PlaySoundEffect (who, 0x0021);
   PrintText(balao, "O liquido esta em ebulicao.");
   sleep(5);
   PlaySoundEffect (who, 0x241);
   sleep(10);
   PrintText(beker, "O liquido esta no beker.");
   
   SetObjProperty(beker, "pocao", extrato);
   SetObjProperty(beker, "strength",  cint(GetObjProperty(balao, "strength")));
   
   EraseObjProperty(balao, "extrato");
   EraseObjProperty(balao, "strength");
   
   item.movable := 1;
   balao.movable := 1;
   beker.movable := 1;
   
endfunction
