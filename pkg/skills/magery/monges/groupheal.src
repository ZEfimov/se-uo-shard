use uo;
use os;
use vitals;

include ":magery:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";
include "include/tileEffects";
include ":charactercreation:habilidades";
include "include/damage";
include ":ghaia:ghaiaUtil";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
	params := 0; // No longer needed

	if (!targ)
		return;
	endif

	var oldx := who.x;
	var oldy := who.y;
	var gliphs := array{};
	var i := 1;
	var success := 0;
	
	while (i <= 3)
		if (who.x != oldx || who.y != oldy)
			PrintTextPrivate(who, "'Magia Cancelada!'", who, SSM_FAIL);
			break;
		endif
		PerformAction(who, ANIM_CAST_AREA);
		
		if (i == 1)
			CreateRandomGliph(gliphs, targ.x-2, targ.y-2, targ.z);
			sleepms(500);
			CreateRandomGliph(gliphs, targ.x, targ.y-2, targ.z);
			sleepms(500);
			CreateRandomGliph(gliphs, targ.x+2, targ.y-2, targ.z);
		elseif (i == 2)
			CreateRandomGliph(gliphs, targ.x+2, targ.y, targ.z);
			sleepms(500);
			CreateRandomGliph(gliphs, targ.x+2, targ.y+2, targ.z);
			sleepms(500);
			CreateRandomGliph(gliphs, targ.x, targ.y+2, targ.z);
		else
			CreateRandomGliph(gliphs, targ.x-2, targ.y+2, targ.z);
			sleepms(500);
			CreateRandomGliph(gliphs, targ.x-2, targ.y, targ.z);
		endif
		if (i >= 3)
			success := 1;
		endif
		i += 1;
		sleepms(500);
	endwhile
	
	if (success)
		var healed := cint(AP_GetStat(who, "Intelligence")/3);
		if (TemHabilidade(who, "Foco em Criação"))
			healed += 15;
		endif
		
		PlaySoundEffectXYZ(targ.x, targ.y, targ.z, SFX_SPELL_HEAL);
		foreach mobile in ListMobilesNearLocation( targ.x, targ.y, targ.z, 2)
			if (mobile.npctemplate)
				continue;
			endif
			PlayObjectCenteredEffect(mobile, 0x3772, 2, 16);
			SendSysMEssageEx(mobile, "Voce sente seus ferimentos se curarem magicamente.", SSM_INFO);
			healFLS(mobile, healed);
			if (TemHabilidade(who, "Foco em Restauração"))
				HealStamina(mobile, cint(healed*0.8));
			endif
			recalcvitals(mobile);
		endforeach
	endif
	
	sleep(1);
	foreach gliph in gliphs
		var destro := DestroyItem(gliph);
	endforeach
	
	return 1;
endprogram 
