use uo;
use os;

include ":magery:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedscripts:timedScripts";
include ":tn:cooldown";
include "include/damage";

program SpellScript(params)
	var who := params[1];
	var info := params[2];
	params := 0; // No longer needed

	var targ := who.weapon;
	if (!targ)
		SendSysMessageEx(who, "Cancelado", SSM_FAIL);
		return;
	endif

	var time := 180;
	
	var dmg := struct;
	dmg.+type := DMG_FIRE;
	dmg.+endtime := readgameclock() + time;
	dmg.+bonus := AP_GetStat(who, "Intelligence")/400.0;
	if  (dmg.bonus <= 0.1)
		dmg.bonus := 0.1;
	endif

	var members_list := who.party.members;
	if (!(who in members_list))
		if (!members_list)
			members_list := array{};
		endif
		members_list.append(who);
	endif
	
	foreach member in members_list
		var targ := member.weapon;
		if (targ.isA(POLCLASS_WEAPON) && Distance(member, who) <= 10)
			var encantada := GetObjProperty(targ, "#elementalweapon");
			if (encantada != error)
				if (readgameclock() < encantada.endtime )
					continue;
				endif
				if ( GetCooldown(targ, "blessweapon") > 0 )
					continue;
				endif
			endif
			dmg.serial := member.serial;
			
			SetObjProperty(targ, "#elementalweapon", dmg);
			start_script( ":magery:elementalweapon", {targ, time, DMG_FIRE} );
			PlaySoundEffect(targ, SFX_BLESS);
			PrintTextPrivate(member, "'Sua arma esta coberta de chamas!", member, 36);
		endif
	endforeach

endprogram    
 