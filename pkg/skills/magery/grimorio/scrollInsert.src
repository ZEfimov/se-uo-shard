/* $Id$
 *
 */
use uo;
use os;
use cfgfile;

include "include/say";
include "include/arrays";

//tipo do book eh pra saber se pode inserir scrolls de um certo tipo em um certo book
function GetBookType(book)
	var type := GetObjProperty(book, "Booktype");
	if (type == error)
		return "mage";
	else
		return type;
	endif

endfunction 

function CanInsertScroll(book, scroll)
	var booktype := GetBookType(book);
	var cfg := ReadConfigFile(":magery:itemdesc");
	var scrolltype := cfg[scroll.objtype].SpellType;

	//printtextabove(book, " " + bookcfg[booktype].spelltype + " " + scrolltype);

	if ( lower(GetBookType(book)) == lower(scrolltype))
		return 1;
	else
		return 0;
	endif

endfunction

program on_insert(who, book, movetype, inserttype, item, existing_stack, amt);
	amt            := amt;
	existing_stack := existing_stack;
	movetype       := movetype;
	inserttype     := inserttype;

	if( book.movable == 0 )
		SendSysMessage(who, "Cancelado.");
		MoveItemToContainer(item, who.backpack);
		return 0;
	elseif( !ReserveItem(book) || !ReserveItem(item) )
		MoveItemToContainer(item, who.backpack);
		SendSysMessage(who, "O livro esta em uso.");
		return 0;
	else

		if (!CanInsertScroll(book, item))
			SendSysMessageEx(who, "Esse nao e o livro certo para esse tipo de pergaminho." , SSM_FAIL);
			MoveItemToContainer(item, who.backpack);
			return 0;
		endif

		var spell_list := GetObjProperty(book, "spells");
		var spellid   := GetObjProperty(item, "spellinfo");
		if (FindInArray(spell_list, spellid))
				SendSysMessageEX(who,"O grimorio ja possui esta magia.", SSM_FAIL);
				MoveItemToContainer(item, who.backpack);
				return 0;
		endif

		if( GetObjProperty(item, "spellinfo") )
			if ( addScroll(who, book, item) == 1)
				DestroyItem(item);
			endif
		else
			SendSysMessage(who, "Este nao parece um pergaminho de magia.");
			MoveItemToContainer(item, who.backpack);
			return 0;
		endif
	endif
	
	return 1;
endprogram


function addScroll(who, book, rune)
	var spell_list := GetObjProperty(book, "spells");
	if (spell_list == error)
		spell_list := array;
	elseif( spell_list.Size() > 15 )
		SendSysMessage(who,"Este livro ja esta cheio.");
		MoveItemToContainer(rune, who.backpack);
		return 0;
	elseif( !ReserveItem(rune) )
		SendSysMessage(who,"Cancelado");
		MoveItemToContainer(rune, who.backpack);
		return 0;
	endif
	
	Set_Critical(1);

	spell_list.Append(GetObjProperty(rune, "spellinfo"));

	SetObjProperty(book, "spells", spell_list);

	SendSysMessage(who, "Voce adicionou o pergaminho no livro.");
	ReleaseItem(rune);
	DestroyItem(rune);
	Set_Critical(0);

	return 1;
endfunction
