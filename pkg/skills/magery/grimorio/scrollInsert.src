/* $Id$
 *
 */
use uo;
use os;
use cfgfile;

include "include/say";
include "include/arrays";
include ":magery:tnmagery";
include ":crafting:recipes";
include ":charactercreation:feats";

//tipo do book eh pra saber se pode inserir scrolls de um certo tipo em um certo book
function GetBookType(book)
	var type := GetObjProperty(book, "Booktype");
	if (type == error)
		return "mage";
	else
		return type;
	endif

endfunction 

program on_insert(who, book, movetype, inserttype, item, existing_stack, amt);
	amt            := amt;
	existing_stack := existing_stack;
	movetype       := movetype;
	inserttype     := inserttype;

	if( book.movable == 0 )
		SendSysMessageEx(who, "Cancelado.");
		MoveItemToContainer(item, who.backpack);
		return 0;
	elseif( !ReserveItem(book) || !ReserveItem(item) )
		MoveItemToContainer(item, who.backpack);
		SendSysMessageEx(who, "O livro está em uso.");
		return 0;
	endif

	addScroll(who, book, item);
	return 1;
endprogram

function addScroll(who, book, scroll)
	var spell_id    := GetObjProperty(scroll, "spell_id");
	var spell_power    := GetObjProperty(scroll, "spell_power");
	var spell_list := GetObjProperty(book, "spells");
	var spells_powered := GetObjProperty(book, "spells_powered");
	if (spell_list == error)
		spell_list := array{};
	endif

	if (typeof(spells_powered) != "Dictionary")
		spells_powered := dictionary{};
	endif

   if (!spells_powered[spell_id]) spells_powered[spell_id] := 0; endif

	if (!spell_id)
		SendSysMessageEx(who, "Este não parece um pergaminho de magia.", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif( spell_list.Size() > 15 && !FindInArray(spell_list, spell_id) )
		SendSysMessageEx(who,"Este livro ja esta cheio.", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif( !ReserveItem(scroll) )
		SendSysMessageEx(who,"Cancelado", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif (spells_powered[spell_id] >= 13)
		SendSysMessageEX(who,"Esta magia já está nivelada ao máximo no grimório", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
   elseif (TS_GetCooldown(who, "learning_recipe") && !who.cmdlevel)
      SendSysMessageEx(who, "Sua mente ainda esta absorvendo a ultima receita que leu.", SSM_FAIL);
      MoveItemToContainer(scroll, who.backpack);
      return 0;
	endif

   var spellinfo := spell_id;
   WriteSpellInfo(spellinfo);

   if (spells_powered[spell_id]+1 > AP_GetTrueSkill(who, "Magia_de_{}".format(spellinfo.type)))
		SendSysMessageEX(who,"Você é incapaz de melhorar/aprender essa magia por enquanto.", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
   elseif (spell_power && spells_powered[spell_id]+1 > spell_power)
		SendSysMessageEX(who,"O nível deste pergaminho é inferior ao que do seu grimório.", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
   endif

   var recipe_charges := (spellinfo.circle * 2) -1;

   if (recipe_charges < 1)
      recipe_charges := 1;
   endif

   if (!CheckCharges(who, scroll, spellinfo.difficulty, recipe_charges))
		SendLearnMessage(who);
      MoveItemToContainer(scroll, who.backpack);
      return 0;
   endif
	
	Set_Critical(1);
   if (!FindInArray(spell_list, spell_id))
      spell_list.Append(spell_id);
      spells_powered[spell_id] += 1;

      SetObjProperty(book, "spells", spell_list);
	   SetObjProperty(book, "spells_powered", spells_powered);
	   SendSysMessageEx(who, "Você adicionou o pergaminho no livro.", SSM_INFO);
   else
      if (cint(spells_powered[spell_id]) <= 0) spells_powered[spell_id] := 1; endif
      spells_powered[spell_id] += 1;
	   SetObjProperty(book, "spells_powered", spells_powered);
      SendSysMessageEx(who, "Você melhorou sua magia no livro.", SSM_INFO);
   endif

	ReleaseItem(scroll);
	DestroyItem(scroll);
	Set_Critical(0);

	return 1;
endfunction
