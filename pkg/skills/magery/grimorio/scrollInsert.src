/* $Id$
 *
 */
use uo;
use os;
use cfgfile;

include "include/say";
include "include/arrays";
include ":magery:tnmagery";
include ":crafting:recipes";
include ":charactercreation:feats";

//tipo do book eh pra saber se pode inserir scrolls de um certo tipo em um certo book
function GetBookType(book)
	var type := GetObjProperty(book, "Booktype");
	if (type == error)
		return "mage";
	else
		return type;
	endif

endfunction 

program on_insert(who, book, movetype, inserttype, item, existing_stack, amt);
	amt            := amt;
	existing_stack := existing_stack;
	movetype       := movetype;
	inserttype     := inserttype;

	if( book.movable == 0 )
		SendSysMessageEx(who, "Cancelado.");
		MoveItemToContainer(item, who.backpack);
		return 0;
	elseif( !ReserveItem(book) || !ReserveItem(item) )
		MoveItemToContainer(item, who.backpack);
		SendSysMessageEx(who, "O livro está em uso.");
		return 0;
	endif

	addScroll(who, book, item);
	return 1;
endprogram

function addScroll(who, book, scroll)
	var spell_id    := GetObjProperty(scroll, "spell_id");
	var spell_list := GetObjProperty(book, "spells");
	if (spell_list == error)
		spell_list := array;
	endif
	
	if (!spell_id)
		SendSysMessageEx(who, "Este não parece um pergaminho de magia.", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif( spell_list.Size() > 15 )
		SendSysMessageEx(who,"Este livro ja esta cheio.", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif( !ReserveItem(scroll) )
		SendSysMessageEx(who,"Cancelado", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif (FindInArray(spell_list, spell_id))
		SendSysMessageEX(who,"O grimorio já possui esta magia.", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	endif

   var spellinfo := spell_id;
   WriteSpellInfo(spellinfo);

   var recipe_charges := spellinfo.circle * 2;
   if (HaveFeat(who, "Tradição Arcana {}".format(spellinfo.type)))
      recipe_charges := recipe_charges / 2;
   endif

   if (!CheckCharges(who, scroll, recipe_charges))
		SendLearnMessage(who);
      return 0;
   endif
	
	Set_Critical(1);

	spell_list.Append(spell_id);

	SetObjProperty(book, "spells", spell_list);

	SendSysMessageEx(who, "Você adicionou o pergaminho no livro.", SSM_INFO);
	ReleaseItem(scroll);
	DestroyItem(scroll);
	Set_Critical(0);

	return 1;
endfunction
