use os;
use uo;
include "include/say";
include "include/arrays";
include ":magery:tnmagery";
include ":attributes:attributes";

program useWeapon(who, weapon)
   var active_spell; 
   if (!weapon)
      active_spell := who[3];
      weapon       := who[2];
      who          := who[1];
   endif

   var weapon_owner := GetObjProperty(weapon, "serialid");
   if (who.cmdlevel < 2 && !HaveSkill(who, "Arcanismo"))
		SendSysMessageEx(who, "Voce nao tem conhecimento arcano.", SSM_FAIL);
		return;
   elseif (who.weapon != weapon)
		SendSysMessageEx(who, "Precisa estar segurando a arma para usar magias.", SSM_FAIL);
		return;
   elseif (weapon_owner && weapon_owner != who.serial)
		SendSysMessageEx(who, "Voce nao sabe usar equipamentos magicos de outra pessoa.", SSM_FAIL);
		return;
   endif

   if (!active_spell)
      active_spell := GetObjProperty(weapon, "active_spell");
   endif
   
   var spell_list := GetObjProperty(weapon, "spell_list");
   if (!(active_spell in spell_list))
      SendSysMessageEx(who, "Nenhuma magia ativa na arma", SSM_FAIL);
      SendSysMessageEx(who, "Clique com o botao direito sobre a arma e selecione 'Trocar Magia'", SSM_FAIL);
      return 0;
   endif

   var elems := FindElemInArray(spell_list, active_spell);
   WriteSpellInfo(active_spell);
   if (elems.Size() > 1)
      active_spell.powered := elems.Size()-1;
   endif
	Start_Script("cast_spell", array{who, active_spell, weapon});
endprogram