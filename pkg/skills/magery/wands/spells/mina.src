use uo;
use os;
use cfgfile;

include "include/client";
include "include/sounds";
include "include/facings";
include "include/say";
include "include/damage";
include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":magery:basicMagery";

var item_place := array{
    array{ 0x307f,  0, 0},  
    array{ 0x3083, -1, -1},  
    array{ 0x3082,  0, -1},  
    array{ 0x3081,  1, -1},  
    array{ 0x3080, -1, 0},  
    array{ 0x307e,  1, 0},  
    array{ 0x307d, -1, 1},  
    array{ 0x307c,  0, 1},  
    array{ 0x307b,  1, 1}
};

program animateSword(params)
	var who := params[1];
	var spellinfo := params[2];
	var targ := params[3];
	params := 0; // No longer needed

   var difficulty := spellinfo.dice_roll+7;
   var duration := 60;
   var dice_dmg;

   var amount := 1;
   if (!who.npctemplate)
      dice_dmg := MageDiceTable("A", spellinfo.powered);
      if (spellinfo.powered >= 4) amount += 1; endif
      if (spellinfo.powered >= 8) amount += 1; endif
      if (spellinfo.powered >= 10) amount += 1; endif
   else
      difficulty := spellinfo.difficulty;
      dice_dmg := spellinfo.dice_dmg;
      if (spellinfo.duration) duration := spellinfo.duration; endif
   endif


   for i := 1 to amount
      if (!targ)
         if (who.npctemplate)
            return;
         endif
         targ := TargetCoordinates(who);
         if (!targ) break; endif
      endif

      if (!who.npctemplate && !CheckLOSAt(who, targ.x, targ.y, targ.z+5));
         SendSysMessageEx(who, "Não tem visão do local", SSM_FAIL);
         amount +=1;
         continue; 
      endif

      PlaySoundEffect(who, 0x1F0);
      var first;
      var all_links := array{};
      foreach mine in item_place
         var item := CreateItemAtLocation(targ.x+mine[2], targ.y+mine[3], targ.z, 0xA094);
         if (!first) 
            first := item;
         else
            item.linkTo(first.serial);
         endif

         all_links.append(item.serial);

         item.movable := 0;
         item.graphic := mine[1];
         SetObjProperty(item, "DamageDice", dice_dmg);
         item.SetOwner(who);
         item.SetDifficulty(difficulty);
         item.SetDuration(duration);
         if (spellinfo.type == "Fogo")
            item.SetEffect(DMG_FIRE);
            item.color := 2039;
         else
            item.color := 2051;
         endif
         sleepms(2);
      endforeach
      
      first.linkTo(all_links);

      targ := 0;
   endfor

	return 1;
endprogram

//  sound
// 
// 2039 - Fogo HUE