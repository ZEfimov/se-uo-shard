use uo;
use os;
use cfgfile;

include "include/client";
include "include/sounds";
include "include/facings";
include "include/say";
include "include/damage";
include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":magery:basicMagery";
include ":combat:combat";

program animateSword(params)
	var who := params[1];
	var spellinfo := params[2];
	var targ := params[3];
	params := 0; // No longer needed

   PlaySoundEffect(who, 0x2F5);
   var coords := GetCoordsInLine(who.x, who.y, targ.x, targ.y);
   foreach c in coords
      c.z := who.z;
      LightningEffect(c);
      sleepms(2);
   endforeach
   sleepms(100);
   
   var mobs := ListMobilesNearLocationEx( targ.x, targ.y, targ.z, 5, LISTEX_FLAG_NORMAL);
   mobs.reverse();

   var sum := 0;
   foreach mobile in mobs
      if (_mobile_iter >= 3+sum)
         break;
      elseif (CheckifIsAlly(who, mobile))
         sum += 1;
         continue;
      endif

      var dmg_dice := MageDiceTable("B", spellinfo.powered);
      if (rollResistDice(mobile, CONSTITUTION) > spellinfo.dice_roll+2)
         SendSystemReport(mobile, "*resistiu*", SSM_FAIL, who);
         SendSystemReport(mobile, "*resistiu*", SSM_INFO);
         DamageFLS(mobile, (RandomDiceRoll(dmg_dice)+2)/2, DMG_ENERGY, who);
      else
         DamageFLS(mobile, RandomDiceRoll(dmg_dice)+2, DMG_ENERGY, who);
      endif
      PlayLightningBoltEffect(mobile);
      sleepms(2);
   endforeach

	return 1;
endprogram

//  sound