use uo;
use os;

include ":magery:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedscripts:timedScripts";
include "include/damage";
include ":magery:basicmagery";
var stalagmites := array{0x08E0, 0x08E1, 0x08E4, 0x08E5, 0x08E0};


program SpellScript(params)
	var who       := params[1];
   var spellinfo := params[2];
   var dice_roll := params[2].dice_roll;
	var targ      := params[3];
	params := 0; // No longer needed
	
   var direction := GetFacing(who.x, who.y, targ.x, targ.y);
   var face := cint(direction / 2);
   if (face > 4)
      face /= 2;
   endif

   var flame := 0x3996;
   if (face % 2)
      flame := 0x398C;
   endif

   var pos := GetCoordsInLine(who.x, who.y, targ.x, targ.y);
   pos.Erase(1);
   var hostiles := ListHostiles(who);
   var z := who.z;

   var dmg_dice := RandomDiceRoll(MageDiceTable("C", spellinfo.powered));
   foreach p in pos
      if (targ.z > z)
         z += 1;
      elseif (targ.z < z)
         z -= 1;
      endif
      var item := CreateItemAtLocation(p.x, p.y, z, cint(flame));
      item.SetDuration(1);
      foreach mobile in ListMobilesNearLocationEx( item.x, item.y, item.z, 0, LISTEX_FLAG_NORMAL|LISTEX_FLAG_HIDDEN)
			if (((!(mobile in who.party.members) && mobile != who) || mobile in hostiles))
            if (rollResistDice(mobile, DEXTERITY) > dice_roll+3)
               SendSystemReport(mobile, "*esquivou*", SSM_FAIL, who);
               SendSystemReport(mobile, "*esquivou*", SSM_INFO);
            else
               DamageFLS(mobile, dmg_dice, DMG_FIRE, who);
            endif
         endif
      endforeach
      sleepms(100);
   endforeach


	return 1;
endprogram 