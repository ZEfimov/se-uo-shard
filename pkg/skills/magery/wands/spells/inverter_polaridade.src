use uo;
use os;
use cfgfile;

include "include/client";
include "include/sounds";
include "include/say";
include "include/shapes";
include "include/damage";
include ":attributes:attributes";
include ":timedscripts:timedScripts";

program animateSword(params)
	var who := params[1];
	var spellinfo := params[2];
	var targ := params[3];
	params := 0; // No longer needed

   var me_pos := struct{ "x" := who.x, "y" := who.y, "z" := who.z};
   MoveObjectToLocation(who, targ.x, targ.y, targ.z, targ.realm, MOVEOBJECT_FORCELOCATION);
   MoveObjectToLocation(targ, me_pos.x, me_pos.y, me_pos.z, who.realm, MOVEOBJECT_FORCELOCATION);

   var hostiles := ListHostiles(who);
   foreach mobile in (ListMobilesNearLocationEx( targ.x, targ.y, targ.z, 3, LISTEX_FLAG_NORMAL|LISTEX_FLAG_HIDDEN))
      sleepms(2);
      if ((!(mobile in who.party.members) && mobile != who) || mobile in hostiles)
         if (rollResistDice(mobile, CONSTITUTION) > spellinfo.dice_roll)
            SendSystemReport(mobile, "*resistido*", SSM_FAIL, who);
            SendSystemReport(mobile, "*resistido*", SSM_INFO);
            continue;
         endif
         DamageFLS(mobile, RandomDiceRoll("3d8"), DMG_ENERGY, who);
      endif
   endforeach
   PlaySoundEffect(targ, 0x51E);

   var coords := GetCoordsInSquare(targ.x, targ.y, 2);
   foreach p in coords
      p.z := targ.z;
      LightningEffect(p);
      sleepms(2);
   endforeach
	return 1;
endprogram
