use uo;
use os;
use cfgfile;

include "include/client";
include "include/sounds";
include "include/say";
include ":attributes:attributes";
include ":timedscripts:timedScripts";

program animateSword(params)
	var who := params[1];
	var roll_dice := params[2].dice_roll;
	var targ := params[3];
	params := 0; // No longer needed

   var item := CreateItemAtLocation(targ.x, targ.y, targ.z, 0x37C4);
   var mobiles := ListMobilesInLineOfSight( item, 5);

   var dmg := RandomDiceRoll("2d4") + AP_GetStat(who, INTELLIGENCE, RETURN_BONUS);
   foreach mob in mobiles
      sleepms(2);
      if (mob == who || mob in who.party.members)
         continue;
      endif
      if (rollResistDice(mob, DEXTERITY) <= roll_dice)
         PrintText(mob, "*cegado*", SSM_HARMFUL);
         TS_StartTimer(mob, "blinded", 10);
         DamageFLS(mob, dmg, DMG_ENERGY, who);
      else
         SendSystemReport(mob, "*resistido*", SSM_HELPFUL, who);
         DamageFLS(mob, Floor(dmg/2), DMG_ENERGY, who);
      endif
   endforeach

   sleep(1);
   DestroyItem(item);
	return 1;
endprogram
