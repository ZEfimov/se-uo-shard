/* $Id$
 *
 */
use uo;
use os;
use cfgfile;

include ":charactercreation:habilidades";
include "include/say";
include "include/arrays";
include ":crafting:recipes";

program on_insert(who, book, movetype, inserttype, item, existing_stack, amt);
	amt            := amt;
	existing_stack := existing_stack;
	movetype       := movetype;
	inserttype     := inserttype;
	if( book.movable == 0 )
		SendSysMessageEx(who, "Cancelado.", SSM_INFO);
		MoveItemToContainer(item, who.backpack);
		return 0;
	elseif( !ReserveItem(book) || !ReserveItem(item) )
		MoveItemToContainer(item, who.backpack);
		SendSysMessageEx(who, "O livro esta em uso.", SSM_FAIL);
		return 0;
	elseif( item.objtype != 0xef1f)
		SendSysMessageEx(who, "Voce nao pode colocar isto no livro.", SSM_FAIL);
		MoveITemToContainer(item, who.backpack);
		return 0;
	endif

	if( GetObjProperty(item, "objtype") )
		return addScrollOnBook(who, book, item);
	endif

	SendSysMessageEx(who, "Voce nao pode colocar isto no livro.", SSM_FAIL);
	MoveItemToContainer(item, who.backpack);
	return 0;
endprogram


function addScrollOnBook(who, book, recipe)
	var recipe_list := GetObjProperty(book, "recipes");
	if (recipe_list == error)
		recipe_list := array;
	endif

	var new_recipe  := GetObjProperty(recipe, "objtype");
	var potions_cfg := ReadConfigFile(":cooking:recipes");
	if(!potions_cfg[new_recipe]);
		SendSysMessageEx(who, "Receita invalida!", SSM_FAIL);
		MoveItemTocontainer(recipe, who.backpack);
		return 0;
	endif

	var diff := GetConfigInt(potions_cfg[new_recipe], "difficulty");
	
	if (FindInArray(recipe_list, new_recipe))
		SendSysMessageEx(who, "Este livro ja contem esta receita.", SSM_FAIL);
		MoveItemTocontainer(recipe, who.backpack);
		return 0;
	elseif( recipe_list.Size() > 15 )
		SendSysMessageEx(who,"Este livro ja esta cheio.", SSM_FAIL);
		MoveItemToContainer(recipe, who.backpack);
		return 0;
	elseif( !ReserveItem(recipe) )
		SendSysMessageEx(who,"Cancelado", SSM_INFO);
		MoveItemToContainer(recipe, who.backpack);
		return 0;
	elseif (TS_GetCooldown(who, "learning_recipe"))
      SendSysMessageEx(who, "Sua mente ainda esta absorvendo a ultima receita que leu.", SSM_FAIL);
      return 0;
	elseif (!CheckCharges(who, recipe, diff))
		SendLearnMessage(who);
		return 0;
	endif

	Set_Critical(1);
	recipe_list.Append(new_recipe);

	SetObjProperty(book, "recipes", recipe_list);

	ReleaseItem(recipe);
	DestroyItem(recipe);
	Set_Critical(0);

	SendSysMessageEx(who, "Voce adicionou o pergaminho no livro.", SSM_INFO);
	return 1;
endfunction
