use uo;
use os;

include "include/client";
include "include/say";
include ":attributes:include/attributeConstants";
include ":attributes:include/attributes";

Program UseGrower(who, grower)
	if ( !ReserveItem(grower) )
		SendSysMessageEx(who, "Alguem já esta colhendo isto.");
		return;
	endif

	var where := struct;
	where.+x := who.x;
	where.+y := who.y;

	var num_loops := RandomDiceRoll("1d3");
	
	for ( num_loops; num_loops>=0; num_loops:=num_loops-1 )
		if ( !CanPick(who, grower, where) )
			break;
		elseif ( !HarvestPlant(who, grower) )
			break;
		endif
		
		PerformAction(who, ANIM_BOW);
		sleepms(3000);
	endfor
	
	SendSysMessageEx(who, "Você parou de colher a planta...");
endprogram

function HarvestPlant(who, grower)
	var difficulty := CInt(grower.GetDifficulty());
   if (!difficulty) difficulty := 50; endif
	grower.SetProduceAmount(grower.GetProduceAmount()-1);
	
	// SendSysMessageEx(who, "dificuldade " + difficulty);

	var test := RandomInt(100);	
	if ( test >= difficulty )
		CreateItemInBackPack(who, grower.GetProduceType(), 1);
		SendSysMessageEx(who, "Você guardou os frutos na sua mochila.");
      who.SetAchievement("plant", 1);
		var seed_chance := grower.GetSeedDice();
		var roll := RandomInt(100)+1;
		if ( roll <= seed_chance )
			SendSysMessageEx(who, "Você pegou uma semente.");
			CreateItemInBackPack(who, grower.GetGrowerType(), 1);
		else
			if ( RandomInt(15) == 2 && !GetObjProperty(grower, "vaseplant") )
				SendSysMessageEx(who, "Você pegou um pouco de adubo.");
				CreateItemInBackPack(who, "fertiledirt", 1);
			endif
		endif
		PlaySoundEffect(who, 0x0050);
		return 1;
	else
		SendSysMessageEx(who, "Você não consegui pegar nada útil.");
		return 0;
	endif
endfunction

function CanPick(who, grower, where)
	if ( who.hidden )
		SendSysMessageEx(who, "Você parou de colher para ficar escondido.");
		return 0;
	elseif ( grower.IsDead() )
		SendSysMessageEx(who, "A planta esta morta.");
		return 0;
	elseif ( !grower.CanHarvest() )
		SendSysMessageEx(who, "Esta planta não parece estar pronta para ser colhida.");
		return 0;
	elseif ( grower.GetProduceAmount() <= 0 )
		SendSysMessageEx(who, "Esta planta não possui nada para ser colhida.");
		return 0;
	elseif ( GetEquipmentByLayer(who, LAYER_MOUNT) )
		SendSysMessageEx(who, "Você não pode colher montado.");
		return 0;
	elseif ( (where.x != who.x) || (where.y != who.y) )
		SendSysMessageEx(who, "Você parou de colher.");
		return 0;
	else
		return 1;
	endif
endfunction
