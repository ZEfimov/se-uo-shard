use cfgfile;
use uo;
 include ":attributes:attributes";
include "include/say";
include ":timedscripts:timedScripts";
include ":loot:magicLoot";
//include "include/damage";
include ":charactercreation:habilidades";

var item_config     := ReadConfigFile(":*:itemdesc");

program skill_armslore(who)

	SendSysMessageEx(who, "Escolha um objeto para avaliar.", SSM_REQUEST);
	var item := Target(who, TGTOPT_CHECK_LOS);

	if (!item)
		SendSysMEssageEx(who, "Cancelado.", SSM_FAIL);
		return;
	endif

	if ( ! item.isa(POLCLASS_ITEM))
		return;
	endif

	if (item.graphic == 3849)
		PrintText(who, "*analisando um frasco*");
		if (SkillCheck(who, RESEARCH, -1) > 0)
			var cfg := ReadConfigFile(":*:itemdesc");
			var entry := cfg[item.objtype];
			if (entry)
				SetObjProperty(item, "pocaoname", entry.Type);
				SendSysMessageEx(who, "Voce identificou o liquido do frasco: " + entry.Type, SSM_INFO);
			endif
			cfg := 0;
			entry := 0;
			var whos := GetObjProperty(item, "magicwho");
			if (whos == error)
				whos := array;
			endif
			if ( !(who.serial in whos))
				whos.append(who.serial) ;
				SetObjProperty(item, "magicwho", whos);
			endif
			var backup := GetAmount(item);
			SubtractAmount(item, backup-1);
			SetName(item, item.desc);
			AddAmount(item, backup-1);
		else
			SendSysMessageEx(who, "Voce nao esta certo..", SSM_FAIL);
		endif
	else
		PrintText(who, "*analisando um item*");
		
		if (SkillCheck(who, RESEARCH, -1) > 0)
			
			if (item_config[item.objtype].PlantName)
				var seed_habilidade, seed_estacao := "";
				var seed_estacao_list := GetConfigStringArray( FindConfigElem(item_config, item.objtype) , "DestroyIn" );
				var seed_difficulty := item_config[item.objtype].PlantDifficulty;
				
				if (seed_difficulty >= 90)
					seed_habilidade := "Agricultura de Reagentes";
					seed_difficulty := "Alta";
				elseif (seed_difficulty >= 65)
					seed_habilidade := "Agricultura Avancada";
					seed_difficulty := "Média";
				elseif (seed_difficulty >= 20)
					seed_habilidade := "Agricultura Basica";
					seed_difficulty := "Baixa";
				else
					seed_habilidade := "Nada";
					seed_difficulty := "Facilissimo";
				endif
				
				foreach estacao in seed_estacao_list
					case (cint(estacao))
						1: seed_estacao := seed_estacao + " Verao;";
						2: seed_estacao := seed_estacao + " Outono;";
						3: seed_estacao := seed_estacao + " Inverno;";
						5: seed_estacao := seed_estacao + " Primavera;";
					endcase
				endforeach
				
			
				SendSysMessageEx(who, "Esta semente nao brota no :" + seed_estacao, SSM_INFO);
				SendSysMessageEx(who, "Parece exigir habilidades em: " + seed_habilidade, SSM_INFO);
				SendSysMessageEx(who, "Sua dificuldade e: " + seed_difficulty, SSM_INFO);
			endif
		
			if (GetObjProperty(item, "magic") != error)
				if (GetObjProperty(item, "equip"))
					ActivateMagicItem(item);
				endif
				var cargas := Cint(GetObjProperty(item, "cargas"));
				if (cargas)
					cargas := cargas + cargas* (AP_GetSkill(who, RESEARCH)/120.0 );
					setobjproperty(item, "cargas", cargas);
				endif
				
				SendSysMessageEx(who, "Voce identificou encantamentos nesse item.", SSM_INFO);
				var whos := GetObjProperty(item, "magicwho");
				if (whos == error)
					whos := array;
				endif
				if ( !(who.serial in whos))
					whos.append(who.serial) ;
					SetObjProperty(item, "magicwho", whos);
				endif
				var cfg := ReadConfigFile(":magery:config/allspells");
				var elem := FindConfigElem(cfg, whos[1]);
				var name := GetConfigString(elem, "Name");
				if (name)
					SetObjProperty(item, "swordmage", name);
				endif
				var backup := GetAmount(item);
				SubtractAmount(item, backup-1);
				SetName(item, item.desc);
				AddAmount(item, backup-1);
				
			elseif(GetObjProperty(item, "craftproperty") != error)
				var craftproperty := GetObjProperty(item, "craftproperty");
				var properties := cint(GetObjProperty(item, "identificado"));
				if (properties >= craftproperty.bonus.size())
					SendSysMessageEx(who, "Voce ja identificou o maximo de propriedades deste item.", SSM_FAIL);
					return;
				elseif(properties == 1 && AP_GetSkill(who, RESEARCH) < 40)
					SendSysMessageEx(who, "Este item parece muito complexo para voce.", SSM_FAIL);
					return;	
				elseif(properties == 2 && AP_GetSkill(who, RESEARCH) < 80)
					SendSysMessageEx(who, "Este item parece muito complexo para voce.", SSM_FAIL);
					return;	
				endif		
				
				if (!properties)
					properties := 0;
				endif
				
				properties+=1;
				SetObjProperty(item, "identificado", properties);
				SendSysMessageEx(who, "Voce identificou novas propriedades para artificie neste item.", SSM_INFO);
				SetName(item, item.desc);
			else
				SendSysMessageEx(who, "Este item nao possui propriedades magicas a serem ativadas.", SSM_INFO);
			endif
		else
			SendSysMessageEx(who, "Voce nao descobriu nada.", SSM_FAIL);
		endif

	endif
endprogram 
