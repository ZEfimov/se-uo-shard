
use uo;
use os;
use util;
include ":itemutils:canAccess";
include ":attributes:attributes";
include ":traps:traps";
include "include/say";
include ":charactercreation:habilidades";

program skillremoveTrap(who)

	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");

	if (!maolivre(who, 2))
		return 0;
	endif

	SendSysMessageEx(who, "Escolha o alvo.", SSM_REQUEST);
	var item := Target(who);
	if(!item)
		SendSysMessageEx(who, "Cancelado.", SSM_FAIL);
		return;
	elseif ( !Accessible(who,item)  )
		SendSysMessageEx(who, "Voce nao alcanca o alvo!", SSM_FAIL);
		return;
	elseif ( Distance(who, item) > 2)
		SendSysMessageEx(who, "Voce esta muito distante.", SSM_FAIL);
		return;
	endif

	var dice_roll := rollAttrDice(who, DEXTERITY) + getProficiencyBonus(who, SABOTAGEM);

	if( GetObjProperty(item, "TrapList") != error )
		if (GetObjProperty(item, "trapSpoted"))
			SendSysMessageEx(who, "Nao tem armadilhas aqui.", SSM_INFO);
			return;
		endif

		var traps := GetObjProperty(item, "TrapList");
		foreach trap in traps
			if (trap.Name == "Magic")
				SendSysMessageEx(who, "Voce nao sabe desarmar essa armadilha.", SSM_FAIL);
				return 1;
			endif
		endforeach

		PrintText(who, "*desarmando armadilha*");
		if (!Wait(who, 5))
			SendSysMessageEx(who, "Voce interrompeu o processo.", SSM_FAIL);
			return;
		endif

		var dif;
		var lvl := Cint(GetObjProperty(item, "level"));
		case (lvl)
			1: dif := 15;
			2: dif := 18;
			3: dif := 23;
			4: dif := 26;
			5: dif := 30;
			default: dif := 18; 
		endcase

		if( dice_roll <= dif )
			SendSysMessageEx( who, "Voce falhou em desarmar a armadilha.", SSM_FAIL);
			// if (!temHabilidade(who, "Especialista em Invasoes") && check < -20)
			// 	if (who.hidden)
			// 		who.hidden := 0;
			// 	endif
			PrintText(who, "*"+who.name+" disparou a armadilha*");
			RunTraps(item, who);
			// endif
			return;
		endif
		SendSysMessageEx( who, "Voce desarmou a armadilha com sucesso.", SSM_INFO);
		RemoveTrap(item, 1);
		item.usescript := "";
		var name := item.desc[" [trapped]"] := "";
		SetName(item, name);
		ExpGain(who, dice_roll, 15);
	elseif ( item.isTrap() )
		if (GetObjProperty(item, "trap_inativa"))
			SendSysMessageEx(who, "A armadilha ja esta desativada", SSM_INFO);
			return;
		endif

		PrintText(who, "*desarmando armadilha*");
		if (!Wait(who, 5))
			SendSysMessageEx(who, "Voce interrompeu o processo.", SSM_FAIL);
			return;
		endif

		if (dice_roll >= 15)
			ExpGain(who, dice_roll, 15);
			SendSysMessageEx( who, "Desativou a armadilha com sucesso.", SSM_INFO);
			SetObjProperty(item, "trap_inativa", 1);
			return;
		endif
		SendSysMessageEx( who, "Falhou em desarmar a armadilha.", SSM_FAIL);
	else
		SendSysMessageEx(who, "Nao tem armadilhas aqui.", SSM_INFO);
	endif
endprogram
