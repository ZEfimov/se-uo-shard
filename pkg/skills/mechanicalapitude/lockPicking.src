use uo;
use os;
use cfgfile;
use util;

include "include/objtype";
//include "include/OLD/sound";
include ":itemutils:canAccess";
include ":attributes:attributeConstants";
include ":attributes:attributes";
include "include/say";
include ":charactercreation:habilidades";
include ":gumps:yesno";

program skill_lockpicking( me, lockpick )
	EraseObjProperty(me, "IsMeditating");
	EraseObjProperty(me, "HealTimer");
	var inhidden;
	if (me.hidden && !TemHabilidade(me, "Maos Rapidas"))
		me.hidden := 0;
	endif
	
	if (!maolivre(me, 2))
		return 0;
	endif

	if(!can_access(me, lockpick))
		return;
	endif

	if(lockpick.movable == 0)
		SendSysMessageEx(me, "Você não pode usar isto.", SSM_FAIL);
		return;
	endif

	if (!ReserveItem(lockpick))
		return;
	endif
	SendSysMessageEx(me, "O que você deseja fazer com isto?", SSM_REQUEST);
	var chest := Target(me);
	if (!chest)
		SendSysMessageEx(me,"Cancelado.", SSM_FAIL);
		return;
	endif
	if ( (!Accessible(me,chest)) || (!Accessible(me,lockpick)) )
		SendSysMessageEx(me, "Você não alcanca o alvo!", SSM_FAIL);
		return;
	endif
	if ( Distance(me, chest) > 2)
		SendSysMessageEx(me, "Você esta muito distante.", SSM_FAIL);
		return;
	endif

	if (GetObjProperty(chest,"level"))
		PickTreasureChest(me, lockpick, chest); //funciona tanto pra chests quando pra portas
	else
		SendSysMessageEx(me, "Esse não e um alvo valido.", SSM_FAIL);
		ReleaseItem(lockpick);
	endif

endprogram

function PickTreasureChest(me, lockpick, chest)
	var lvl := CInt(GetObjProperty(chest,"level"));
	if (!chest.locked)
		SendSysMessageEx(me,"O alvo não parece estar tracado.", SSM_INFO);
		return;
	endif
	PrintText(me, "*arrombando a fechadura*");
	sleepms(250);
	var diff;
	case (lvl)
		1: diff := 15;
		2: diff := 18;
		3: diff := 23;
		4: diff := 26;
		5: diff := 30;
		default: diff := 12; 
	endcase

	var dice_roll := rollAttrDice(me, DEXTERITY) + getProficiencyBonus(me, "Arrombar Fechaduras");
	if ( dice_roll > diff )
		SucessLockPick(me, chest);
      ExpGain(me, diff, dice_roll);
      
      var time_respawn := AddTimeTo(ReadGameClock(), 25+RandomDiceRoll("3d20"), "minutes");
      SetObjProperty(chest, "spawned_time", time_respawn);
	else
		SendSysMessageEx(me, "Você falhou em arrombar a fechadura.", SSM_FAIL);
		PlaySoundEffect(chest,0xef);
		SendSysMessageEx(me,"Sua gazua quebrou!", SSM_FAIL);
		SubtractAmount(lockpick,1);
		return;
	endif
	ReleaseItem(lockpick);
endfunction

function SucessLockPick(me, chest)
	PlaySoundEffect(chest, 0x0200);
	PrintTextAbovePrivate(chest,"*Destrancado!*",me);
	SetObjProperty(chest, "arrombado", "" + me.name);
	SetObjProperty(chest, "locktime", ReadGameClock());
	set_critical(1);
	chest.locked := 0;
	set_critical(0);
	
	if (TemHabilidade(me, "Toque de Ouro"))
		CreateItemInContainer(chest, 0xba63, randomdiceroll("5d25"));
	endif
	
endfunction
