/* $Id: parseCharProfile.src 1144 2006-01-15 11:19:40Z austin $
 * script by MuadDib
 *
 */
use uo;
use os;
use polsys;
use datafile;
use math;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include "include/utils";
include ":gumps:gump_prompts";

CONST PROFILE_MSGTYPE := 0xB8;
CONST PROFILE_TITLE := "Perfil de ";
CONST PROFILE_UPDATE_MODE := 1;
CONST PROFILE_REQUEST_MODE := 0;
CONST HEADER_SIZE := 7;
CONST NULL_SIZE := 1;
CONST UNULL_SIZE := 2;
CONST UCHAR_SIZE := 2;

CONST OFFSET_MSGTYPE := 0;
CONST OFFSET_MSGLEN := 1;
CONST OFFSET_MODE := 3;
CONST OFFSET_SERIAL_OUT := 3;
CONST OFFSET_SERIAL_IN := 4;
CONST OFFSET_TITLE_STR := 7;
CONST OFFSET_CMDTYPE := 8;
CONST OFFSET_NEW_PROFILE_TEXTLEN := 10;
CONST OFFSET_NEW_PROFILE := 12;

program runScript_CharProfileRequest( params )
	var who    := params[1];
	var packet := params[2];

	var mode := packet.GetInt8(OFFSET_MODE); //mode 0 for request, 1 for update
	var id   := packet.GetInt32(OFFSET_SERIAL_IN);  //serial of requested profile
	params := 0; // Not needed anymore.
                                                //who
	var chr := SystemFindObjectBySerial(id);
	if( !chr.isa(POLCLASS_MOBILE) )
		return 0; //don't bother working on nonexistant or items :P
	endif

   while (who.connected)
      var chardata := GetObjProperty(chr, "chardata");
      if (!chardata) return 1; endif

      var altura := GetCharHeight(chardata.height);
      var peso := GetCharWeight(chardata.weight);
      
      var gump := GFCreateGump();
      GFPage(gump, 0);

      GFResizePic(gump, 13, 13, 9380, 275, 233);
      GFGumpPicTiled(gump, 34, 48, 231, 10, 50);
      GFGumpPicTiled(gump, 33, 204, 234, 10, 50);
      GFTextLine(gump, 39, 61, 2212, "Personagem:");
      GFTextLine(gump, 40, 79, 2212, "Altura:");
      GFTextLine(gump, 41, 98, 2212, "Peso:");
      GFTextLine(gump, 115, 62, 1152, "{}".format(chr.name));
      GFTextLine(gump, 90, 79, 1152, "{}".format(altura));
      GFTextLine(gump, 82, 98, 1152, "{}".format(peso));
      GFHTMLArea(gump, 40, 122, 222, 80, "{}".format(chardata.profile), 1, 1);

      if (who.cmdlevel >= 3)
         GFTextLine(gump, 241, 59, 1152, "GM");
         GFAddButton(gump, 245, 83, 2103, 2103, GF_CLOSE_BTN, 2);
         GFAddButton(gump, 245, 99, 2103, 2103, GF_CLOSE_BTN, 3);
      endif
      if (who.cmdlevel >= 3 || who == chr)
         GFAddButton(gump, 145, 202, 2103, 2103, GF_CLOSE_BTN, 4);
      endif

      var res := GFSendGump(who, gump);
      if (res[0] == 2)
         var alturas := array{"Alto", "Mediano", "Baixo"};
         var aws := QuestionsGump(who, array{ struct{ title := "Escolha a altura do presonagem:", radio := alturas, marked := chardata.height }});
         if (aws)
            chardata := GetObjProperty(chr, "chardata");
            chardata.height := aws[1] in alturas;
            SetObjProperty(chr, "chardata", chardata);
         endif
      elseif (res[0] == 3)
         var pesos := array{"Gordo", "Esbelto", "Magro"};
         var aws := QuestionsGump(who, array{ struct{ title := "Escolha o peso do presonagem:", radio := pesos, marked := chardata.weight }});
         if (aws)
            chardata := GetObjProperty(chr, "chardata");
            chardata.weight := aws[1] in pesos;
            SetObjProperty(chr, "chardata", chardata);
         endif
      elseif (res[0] == 4)
         var aws := QuestionsGump(who, array{ struct{ title := "Entre com as características físicas do personagem:", marked := _ifOr(chardata.profile, "") }});
         if (aws)
            chardata := GetObjProperty(chr, "chardata");
            chardata.profile := aws[1];
            SetObjProperty(chr, "chardata", chardata);
         endif
      else 
         return 1;
      endif
      sleepms(2);
   endwhile

	// start_script("parseCharProfile", { character, packet });

	return 1;

endprogram

function GetCharHeight(h)
   case (h)
      1: return "Alto";
      2: return "Mediano";
      3: return "Baixo";
   endcase
endfunction

function GetCharWeight(w)
   case (w)
      1: return "Gordo";
      2: return "Esbelto";
      3: return "Magro";
   endcase
endfunction