use uo;
use math;
use os;
include "include/say";
include ":attributes:attributes";
include ":charactercreation:feats";

program speech_hooks()
  Print( "Hooking Unicode Outgoing..." );
  return 1;
endprogram

const ITEM_ID         := 3;
const TEXT_TYPE       := 9;
const NORMAL_SPEECH   := 0x00;
const EMOTE_SPEECH    := 0x02;
const WHISPER_SPEECH  := 0x08;
const YELL_SPEECH     := 0x09;
const ALLIANCE_SPEECH := 0x0E;
const BROADCAST_SPEECH := 0x01;

exported function handleUCOutgoing( character, byref packet )
	var type := packet.GetInt8(TEXT_TYPE);

	var serial := packet.GetInt32(ITEM_ID);
	var source := SystemFindObjectBySerial(serial);
   
	var length_of_text := ( packet.GetInt16(1) - 49 ) / 2;
	var message_txt := packet.GetUnicodeString(48, length_of_text);
	message_txt := CChrZ(message_txt);

	if (type == 0x06 || (type == 0 && !source))
		return 0;
	endif

   if ((source.hidden == 1 && !character.Enabled("seehidden")) || (source.invisible == 1 && !character.Enabled("seeinvisitems")))
      return 1;
   endif

   if ((!character.dead && source.dead && !character.cmdlevel) || (character.dead && !source.dead))
      return 1;
   endif

	if  ((type == ALLIANCE_SPEECH || type == BROADCAST_SPEECH) && source.cmdlevel >= 1 && GetObjProperty(source, "LastMSG") != message_txt)
		SetObjProperty(source, "LastMSG", message_txt);
		foreach player in GetGlobalProperty("GmsOnlines")
			var sender := source.name;
			SendSysMessageEx(player, "[GMBcast]["+sender+"]: "+message_txt, 0x0063);
		endforeach
		return 0;
	endif

   if (type != YELL_SPEECH)
      if( !CheckLineOfSight(source, character) ) 
         if (!HaveFeatSecondary(character, 38))
            return 1;
         endif

         if (Distance(character, source) > 12 || abs(character.z - source.z ) > 19)
            return 1;
         endif
      endif
   endif

	if (GetObjProperty(source, "metamorfose") || GetObjProperty(source, "mudo") || GetObjProperty(source, "#sleep"))
		if  (type != EMOTE_SPEECH)
			return 1;
		endif
	endif
	
	// SendToSpy(source, message_txt);

	return 0;
endfunction 

exported function handleUCOrequest( character, byref packet )
	var type := packet.GetInt8(4);

	if (getObjProperty(character, "metamorfose") != error)
		if  ( type == NORMAL_SPEECH)
			return 1;
		else
			return 0;
		endif
	endif

	return 0;
endfunction 

function sendToSpy(source, message_txt)
	if (!source.acctname || GetObjProperty(source, "LastMSG") == message_txt )
		return;
	endif

	var unvalid := { message_txt[1, 6], message_txt[1, 9], message_txt[1, 9], message_txt[1, 14], message_txt[1,13]};
	var ignore_messages := array{"*usando ataduras em si mesmo*", "*parou de aplicar bandagens*", "*arrombando a fechadura*", "*desarmando armadilha*", "*preparando um extrato*", "*Errando todas as notas*", "*defende o golpe*"};
	var common_messages := array{"*Tirou", "*Você ", "Você a", "*Pegou"};
   if (unvalid[1] in common_messages || unvalid[2] == "*Guardou " || unvalid[3] == "*Colocou " || unvalid[4] == "*escalpelando " || unvalid[5] == "*Acrescentou " || unvalid[5] == "*Tocando uma " || message_txt in ignore_messages)
		return;
	endif

	foreach player in GetGlobalProperty("GmsOnlines")
		Sleepms(2); 
		player := SystemFindObjectBySerial(Cint(player));
      var sayspy := GetObjProperty(player, "SaySpyOn");
		if ( sayspy && player.cmdlevel >= 3 && player != source)
         if (getObjProperty(player, "lastsayspy") == message_txt)
            continue;
         endif
			var sender := source.name;
			if (sender == "Desconhecido")
				sender := GetObjProperty(source, "realname");
			endif

			if (sayspy == "on")
				SendSysMessageEx(player, "["+sender+"]: "+message_txt);
			elseif(sender in sayspy)
				SendSysMessageEx(player, "["+sender+"]: "+message_txt);
			endif
         SetObjProperty(player, "lastsayspy", message_txt);
		endif
	endforeach
endfunction
